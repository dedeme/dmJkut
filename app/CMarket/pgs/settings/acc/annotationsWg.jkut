// Copyright 11-Jul-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Accounting annotations widget.

import "libdm/menu";
import "libdm/datePicker";
import "wgs/msg";
import "data/cts";
import "data/acc/ann";
import "data/acc/opr";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Anns is ordered by after to before.
/// \<domo>, [<ann>...], [\i->()]|[] -> ()
mk = \wg, Anns, DelFn -> {
  // \s -> <domo>
  tdDate = \d -> return Q("td")
    .klass("border")
    .text(d)
  ;;
  // \s -> <domo>
  tdTp = \t -> return Q("td")
    .klass("border")
    .text(t)
  ;;
  // \s -> <domo>
  tdRest = \s -> return Q("td")
    .klass("border")
    .style("text-align: left;")
    .text(s)
  ;;

  // \<ann> -> [<domo>...]
  addAnn = \Ann -> {
    date = tdDate(time.toIso(time.fromStr(Ann.date)));
    op = Ann.op;

    switch (opr.type(op)){
      opr.seT: return [date, tdTp(II("Sell")), tdRest(
          opr.nick(op) + "|" + math.toIso(opr.stocks(op), 0) + "|" +
          math.toIso(opr.price(op), 4)
        )];
      opr.buT: return [date, tdTp(II("Buy")), tdRest(
          opr.nick(op) + "|" + math.toIso(opr.stocks(op), 0) + "|" +
          math.toIso(opr.price(op), 4)
        )];
      opr.stT: return [date, tdTp(II("In Stock")), tdRest(
          opr.nick(op) + "|" + math.toIso(opr.stocks(op), 0) + "|" +
          math.toIso(opr.price(op), 4)
        )];
      opr.prT: return [date, tdTp(II("Profits")), tdRest(
          math.toIso(opr.amount(op), 2) + "|" + opr.cause(op)
        )];
      opr.feT: return [date, tdTp(II("Fees")), tdRest(
          math.toIso(opr.amount(op), 2) + "|" + opr.cause(op)
        )];
      opr.inT: return [date, tdTp(II("Income")), tdRest(
          math.toIso(opr.amount(op), 2)
        )];
      opr.wiT: return [date, tdTp(II("Withdrawal")), tdRest(
          math.toIso(opr.amount(op), 2)
        )];
      opr.pdT: return [date, tdTp(II("Diff. +")), tdRest(
          math.toIso(opr.amount(op), 2) + "|" + opr.cause(op)
        )];
      opr.ndT: return [date, tdTp(II("Diff. -")), tdRest(
          math.toIso(opr.amount(op), 2) + "|" + opr.cause(op)
        )];
      default: throw ("Unkown operation");
    }
  };

  wg
    .removeAll()
    .add(Q("table")
      .att("align", "center")
      .adds(arr.size(Anns) == 0
        ? [ Q("tr")
            .add(Q("td")
              .klass("frame")
              .text(II("Without Data")))
          ]
        : arr.map(Anns, \Ann ->
            if (DelFn) {
              if (Ann.date[-4:] > "0101" & opr.type(Ann.op) != opr.stT)
                return Q("tr")
                  .add(ui.link(\e -> DelFn!(Ann.id);)
                    .add(ui.img("delete")))
                  .adds(addAnn(Ann));
              else
              return Q("tr")
                .add(Q("td"))
                .adds(addAnn(Ann));
            } else {
              return Q("tr")
                .adds(addAnn(Ann));
            }
          )))
  ;

};
