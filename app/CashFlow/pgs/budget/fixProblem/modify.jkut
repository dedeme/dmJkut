// Copyright 04-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// New entry editor

import "annsEditor";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Constructor.
///   tr: Container row.
///   Plan: Budget plan.
///   HcEntry: Hconta entry.
///   CEntry: Diary entry.
///   ix : Index of CEntry.
///   activated: 'true' if addition is activated.
///   deactivateAllFn: Function to deactivate all operations.
///   updateFn: Diary update. It takes as argument 'ix' and CEntry.
/// \<domo>, <plan>, <cashEntry>, <diaryEntry>, n, b,
///   (\->()), \n,<diaryEntry>->()) ->
///     {deactivate: \->(), updateAccount: \s->()}
mk = \tr, Plan, HcEntry, CEntry, ix, activated, deactivateAllFn, updateFn -> {
  activatedV = [activated];

  imgDiv = Q("div");
  annsDiv = Q("div");

  showV = [[]];


  // Control -------------------------------------------------------------------

  // \ -> ()
  activate = \ -> {
    deactivateAllFn();
    activatedV! = true;
    showV!();
  };

  // \[<diaryAnnotation>.] -> ()
  update = \ -> {
    updateFn(ix, CEntry);
  };

  AnnsEd = annsEditor.mk(annsDiv, CEntry, update);

  // \ -> ()
  deactivate = \ -> {
    activatedV! = false;
    showV!();
  };

  // \s -> ()
  updateAccount = \acc ->
    if (activatedV!) AnnsEd.updateAccount(acc);;

  // View ----------------------------------------------------------------------

  // \ -> ()
  showV! = \ -> {
    if (activatedV!) {
      imgDiv
        .removeAll()
        .klass("frame")
        .add(ui.lightImg("edit"))
      ;
      AnnsEd.active(true);
    } else {
      imgDiv
        .removeAll()
        .add(ui.link(\ev -> activate();)
          .klass("link")
          .add(ui.img("edit")))
      ;
      AnnsEd.active(false);
    }

    tr
      .removeAll()
      .add(Q("td")
        .add(imgDiv))
      .add(Q("td")
        .klass("frameTx")
        .text(CEntry.month))
      .add(Q("td")
        .klass("frameTx")
        .add(Q("div")
          .text(CEntry.desc))
        .add(annsDiv))
      .add(Q("td")
        .klass("frameNm")
        .text(math.toIso(CEntry.am, 2)))

      .add(Q("td"))

      .add(Q("td"))
      .add(Q("td"))
      .add(Q("td"))
    ;
  };

  showV!();

  return {deactivate, updateAccount};
};
