// Copyright 29-Aug-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Plan management page.

import "data/cts";
import "data/plan";
import "data/diary";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Constructor.
///   wg          : Widget.
///   selectedYear: Selected year.
/// \<domo>, s -> ()
mk = async \wg, selectedYear -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "PlanPg",
    rq: "idata",
    year: selectedYear
  });
  Plan = plan.fromJs(Rp.plan);
  Diary = diary.fromJs(Rp.diary);

  OldEntryOp = [];

  showOp = [[]];

  // Control -------------------------------------------------------------------

  // \ -> ()
  updateServerPlan = \ -> {
    return client.ssend({
      prg: cts.appName,
      source: "PlanPg",
      rq: "updatePlan",
      year: selectedYear,
      plan: plan.toJs(Plan)
    });
  };

  // \ -> ()
  updateServerPlanAndDiary = \ -> {
    return client.ssend({
      prg: cts.appName,
      source: "PlanPg",
      rq: "updatePlanAndDiary",
      year: selectedYear,
      plan: plan.toJs(Plan),
      diary: diary.toJs(Diary)
    });
  };

  // \ -> ()
  reload = \ -> {
    arr.clear(OldEntryOp);
    showOp!();
  };

  // \<planEntry> -> ()
  acceptEntry = async \Entry -> {
    if (OldEntryOp) {
      E = OldEntryOp!;
      r = plan.modify(Plan, E.id, Entry, Diary);
      if (r != "") {
        ui.alert(r);
        return;
      }
      diary.changeAcc(Diary, E.id, Entry.id);
      await updateServerPlanAndDiary();
      reload();
    } else {
      r = plan.add(Plan, Entry);
      if (r != "") {
        ui.alert(r);
        return;
      }
      await updateServerPlan();
      reload();
    }
  };

  // \s -> ()
  del = async \id -> {
    if (!ui.confirm(i18n.fmt(II("Delete %0?"), [id]))) return;

    r = plan.del(Plan, id, diary.accs(Diary));
    if (r != "") {
      ui.alert(r);
      return;
    }
    await updateServerPlan();
    reload();
  };

  // \<planEntry> -> ()
  modify = \Entry -> {
    arr.clear(OldEntryOp);
    arr.push(OldEntryOp, Entry);
    showOp!();
  };

  // View ----------------------------------------------------------------------

  // \ -> <domo>
  dataEntry = \ -> {
    Entry = OldEntryOp ? OldEntryOp! : plan.mkEntry(false, "", "");

    opInc = Q("input")
      .att("type", "radio")
      .checked(Entry.isIncome)
      .att("name", "inc")
    ;
    opExp = Q("input")
      .att("type", "radio")
      .checked(!Entry.isIncome)
      .att("name", "inc")
    ;
    idWg = ui.field("desc")
      .att("id", "autofocus")
      .style("width:80px")
      .value(Entry.id)
    ;
    descWg = ui.field("accept")
      .att("id", "desc")
      .style("width:200px")
      .value(Entry.desc)
    ;

    // \ -> <planEntry>
    mkEntry = \ -> return plan.mkEntry(
        opInc.isChecked(),
        str.trim(idWg.getValue())[:6],
        str.trim(descWg.getValue())
      );;

    // \s -> <domo>
    th = \tx -> return Q("td")
        .style(
            "background-color: #c9c9c9;text-align: center;" +
            "border: 1px solid rgb(110,130,150)"
          )
        .html(tx)
      ;;

    // \s -> <domo>
    tdInfo = \tx -> return Q("td")
        .style("color:#C9C9C9;text-align:center")
        .text(tx)
      ;;


    table = Q("table")
      .att("align", "center")
      .add(Q("tr")
        .add(th(II("Type")).att("colspan", "5"))
        .add(th(II("Id")))
        .add(th(II("Description"))))
      .add(Q("tr")
        .add(Q("td").add(opInc))
        .add(Q("td").text(II("Income")))
        .add(Q("td").style("width: 5px"))
        .add(Q("td").add(opExp))
        .add(Q("td").text(II("Expense")))

        .add(Q("td").add(idWg))
        .add(Q("td").add(descWg)))
    ;

    if (OldEntryOp) {
      E = OldEntryOp!;
      table
        .add(Q("tr")
          .add(tdInfo(E.isIncome ? II("Income") : II("Expense"))
            .att("colspan", "5"))
          .add(tdInfo(E.id))
          .add(tdInfo(E.desc)))
      ;
    }

    table
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "7")
          .style("text-align: right")
          .add(Q("button")
            .text(II("Cancel"))
            .on("click", \e -> reload();))
          .add(Q("span").html("&nbsp;"))
          .add(Q("button")
            .att("id", "accept")
            .text(II("Accept"))
            .on("click", \e -> acceptEntry(mkEntry());))))
    ;

    return table;
  };

  // \ -> <domo>
  entryWg = \ -> return Q("div")
    .add(Q("div")
      .klass("head")
      .html(OldEntryOp
        ? II("Modification")
        : II("New Account")))
    .add(dataEntry())
  ;;

  // \ -> <domo>
  withoutAccountsTable = \ -> return Q("table")
    .klass("main")
    .add(Q("tr")
      .add(Q("td")
        .klass("frame")
        .style("text-align: center")
        .text(II("Without Accounts"))))
  ;;

  // \<planEntry> -> <domo>
  mkTr = \Entry -> return Q("tr")
    .add(Q("td")
      .add(ui.link(\e -> del(Entry.id);)
        .add(ui.img("delete"))))
    .add(Q("td")
      .add(ui.link(\e -> modify(Entry);)
        .add(ui.img("edit"))))
    .add(Q("td")
      .style("border: 1px solid rgb(110,130,150)")
      .text(Entry.id))
    .add(Q("td")
      .style("border: 1px solid rgb(110,130,150)")
      .text(Entry.desc))
  ;;

  // \b -> <domo>
  entriesTable = \isIncomes -> {
    div = Q("div")
      .add(Q("div")
        .klass("head")
        .html(isIncomes ? II("Incomes") : II("Expenses")))
    ;

    Es = arr.filter(Plan.entries, \E -> return E.isIncome == isIncomes;);



    if (Es) {
      Trs = []; // [<domo>.]
      for (E = Es) arr.push(Trs, mkTr(E));
      div.add(Q("table")
        .att("align", "center")
        .adds(Trs));
    } else {
      div.add(withoutAccountsTable());
    }

    return div;
  };

  showOp! = \ -> {
    arr.sort(
      Plan.entries, \E1, E2 -> return str.toUpper(E1.id) < str.toUpper(E2.id);
    );

    wg
      .removeAll()
      .add(Q("div")
        .klass("head")
        .html(II("Accounting Plan")))
      .add(Q("table")
        .att("align", "center")
        .add(Q("tr")
          .add(Q("td")
            .att("colspan", "2")
            .klass("frame")
            .style("text-align: center")
            .add(entryWg())))
        .add(Q("tr")
          .add(Q("td")
            .klass("frame")
            .style("vertical-align: top; width:50%")
            .add(entriesTable(true)))
          .add(Q("td")
            .klass("frame")
            .style("vertical-align: top; width:50%")
            .add(entriesTable(false)))))
    ;
  };

  showOp!();
};
