// Copyright 17-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Bills page.

import "libdm/modalBox";
import "data/cts";
import "data/fns";
import "wgs/bar";
import "wgs/form";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// <monthAnn> fields.
month = 0;
place = 1;
amount = 2;

/// \<domo> -> ()
mk = async \wg -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "Bills",
    rq: "idata"
  });
  Anns = Rp.anns;

  boxWg = Q("div");
  Box = modalBox.mk(boxWg, false);

  // Control -------------------------------------------------------------------

  // \<monthAnn> -> ()
  setPlace = \A -> {
    boxWg
      .removeAll()
      .add(form.mk(
          A,
          \ -> modalBox.show(Box, false);,
          async \A2 -> {
            await client.send({
              prg: cts.appName,
              source: "Bills",
              rq: "setPlace",
              month: A2[month],
              place: A2[place]
            });
            mk(wg);
          }
        ))
    ;
    modalBox.show(Box, true);
    Q("#formEntry").e.select();
    Q("#formEntry").e.focus();
  };

  // View ----------------------------------------------------------------------

  // \ -> <domo>
  table = \ -> {
    max = arr.reduce(
      Anns, 0.0, \r, An -> return An[amount] > r ? An[amount] : r;
    );
    tb = Q("table").att("align", "center").klass("border");
    barWg = \A -> return bar.mkWg(
        bar.mk(300, 2, A[amount] / max, "#0080c0", "#ffffff")
      );;

    return Anns
      ? tb
        .add(Q("tr")
          .add(Q("td")
            .klass("header")
            .style("text-align: left")
            .html(II("Month")))
          .add(Q("td")
            .klass("header")
            .style("text-align: left")
            .html(II("Place")))
          .add(Q("td")
            .klass("header")
            .style("text-align: right")
            .html(II("Amount")))
          .add(Q("td").klass("header")))
        .adds(arr.map(Anns, \A -> return Q("tr")
            .add(Q("td")
              .klass("border")
              .style("text-align: left")
              .add(ui.link(\ev -> setPlace(A);)
                .klass("link")
                .text(A[month][4:] + "/" + A[month][:4])))
            .add(Q("td")
              .klass("border")
              .style("text-align: left")
              .html((A[place] ? A[place]! : "") + "&nbsp;"))
            .add(Q("td")
              .klass("border")
              .style("text-align: right")
              .html(fns.cFmt(A[amount])))
            .add(Q("td")
              .add(barWg(A)));))
      : tb
        .add(Q("tr")
          .add(Q("td")
            .style("text-align: center")
            .html(II("Without Data"))))
    ;
  };

  wg
    .removeAll()
    .add(Box.wg)
    .add(Q("table")
      .att("align", "center")
      .add(Q("tr")
        .add(Q("td")
          .add(table()))))
  ;
};
