// Copyright 17-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Stays page.

import "data/cts";
import "data/fns";
import "wgs/bar";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// <accAnn> fields.
date = 0;
description = 1;
amount = 2;

/// \<domo> -> ()
mk = async \wg -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "Stays",
    rq: "idata"
  });
  Anns = Rp.anns;

  // \ -> <domo>
  table = \ -> {
    max = arr.reduce(
      Anns, 0.0, \r, An -> return An[amount] > r ? An[amount] : r;
    );
    tb = Q("table").att("align", "center").klass("border");
    barWg = \A -> return bar.mkWg(
        bar.mk(300, 2, A[amount] / max, "#c080c0", "#ffffff")
      );;

    return Anns
      ? tb
        .add(Q("tr")
          .add(Q("td")
            .klass("header")
            .style("text-align: left")
            .html(II("Date")))
          .add(Q("td")
            .klass("header")
            .style("text-align: left")
            .html(II("Place")))
          .add(Q("td")
            .klass("header")
            .style("text-align: right")
            .html(II("Amount")))
          .add(Q("td").klass("header")))
        .adds(arr.map(Anns, \A -> return Q("tr")
            .add(Q("td")
              .klass("border")
              .style("text-align: left")
              .text(
                  A[date][6:] + "/" +
                  A[date][4:6] + "/" +
                  A[date][:4])
                )
            .add(Q("td")
              .klass("border")
              .style("text-align: left")
              .html(A[description] + "&nbsp;"))
            .add(Q("td")
              .klass("border")
              .style("text-align: right")
              .html(fns.cFmt(A[amount])))
            .add(Q("td")
              .add(barWg(A)));))
      : tb
        .add(Q("tr")
          .add(Q("td")
            .style("text-align: center")
            .html(II("Without Data"))))
    ;
  };

  wg
    .removeAll()
    .add(Q("table")
      .att("align", "center")
      .add(Q("tr")
        .add(Q("td")
          .add(table()))))
  ;
};
