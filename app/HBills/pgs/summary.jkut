// Copyright 17-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Summary page.

/// Stays page.

import "data/cts";
import "data/fns";
import "wgs/bar";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// <accAnn> field.
date = 0;
// <monthAnn> field.
month = 0;
// <accAnn> and <monthAnn> field.
amount = 2;

/// \<domo> -> ()
mk = async \wg -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "Summary",
    rq: "idata"
  });
  Years = Rp.years;
  BillAnns = Rp.billAnns;
  StayAnns = Rp.stayAnns;

  // \ -> <domo>
  table = \ -> {
    // \n, n -> n
    fmax = \n1, n2 -> return n1 > n2 ? n1 : n2;;
    // [[s, n, n]...]
    Rows = arr.map(Years, \y -> return [
        y,
        arr.reduce(
          arr.filter(BillAnns, \A -> return str.starts(A[month], y);),
          0.0, \r, A -> return r + A[amount];
        ),
        arr.reduce(
          arr.filter(StayAnns, \A -> return str.starts(A[date], y);),
          0.0, \r, A -> return r + A[amount];
        )
      ];);
    max = arr.reduce(Rows, 0.0, \r, Row -> return fmax(Row[1] + Row[2], r););
    sumBills = arr.reduce(Rows, 0.0, \r, Row -> return r + Row[1];);
    sumStays = arr.reduce(Rows, 0.0, \r, Row -> return r + Row[2];);
    barWg = \Row -> return bar.mkWg(
        bar.mk(300, 2, (Row[1] + Row[2]) / max, "#c0c080", "#ffffff")
      );;

    return
      Q("table")
        .att("align", "center")
        .klass("border")
        .add(Q("tr")
          .add(Q("td")
            .klass("header")
            .style("text-align: left")
            .html(II("Year")))
          .add(Q("td")
            .klass("header")
            .style("text-align: right")
            .html(II("Bills")))
          .add(Q("td")
            .klass("header")
            .style("text-align: right")
            .html(II("Stays")))
          .add(Q("td")
            .klass("header")
            .style("text-align: right")
            .html(II("Total")))
          .add(Q("td").klass("header")))
        .adds(arr.map(Rows, \R -> return Q("tr")
            .add(Q("td")
              .klass("border")
              .style("text-align: left")
               .text(R[0]))
            .add(Q("td")
              .klass("border")
              .style("text-align: right")
              .html(fns.cFmt(R[1])))
            .add(Q("td")
              .klass("border")
              .style("text-align: right")
              .html(fns.cFmt(R[2])))
            .add(Q("td")
              .klass("border")
              .style("text-align: right")
              .html(fns.cFmt(R[1] + R[2])))
            .add(Q("td")
              .add(barWg(R)));))
        .add(Q("tr")
          .add(Q("td").klass("border"))
          .add(Q("td").klass("border"))
          .add(Q("td").klass("border"))
          .add(Q("td").klass("border"))
          .add(Q("td").klass("border")))
        .add(Q("tr")
          .add(Q("td")
            .klass("border")
            .html(II("Sums") + "&nbsp;"))
          .add(Q("td")
            .klass("border")
            .style("text-align: right")
            .html(fns.cFmt(sumBills)))
          .add(Q("td")
            .klass("border")
            .style("text-align: right")
            .html(fns.cFmt(sumStays)))
          .add(Q("td")
            .klass("border")
            .style("text-align: right")
            .html(fns.cFmt(sumBills + sumStays)))
          .add(Q("td").klass("border")))
    ;
  };


  wg
    .removeAll()
    .add(Q("table")
      .att("align", "center")
      .add(Q("tr")
        .add(Q("td")
          .add(table()))))
  ;
};
