// Copyright 13-Aug-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Program starting.

import "libdm/menu";
import "data/cts";
import "data/all";
import "data/acc";
import "pgs/msgPg";
import "pgs/yearPg";
import "pgs/diaryPg";
import "pgs/cashPg";
import "pgs/accsPg";
import "pgs/summaryPg";
import "pgs/planPg";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// \<domo> -> ()
mk = async \wg -> {
  ok = await client.connect();
  if (!ok) {
    ui.alert(II("KtWeb session is closed.\nAuthenticating from KtWeb:Main."));
    window.location.assign("http://" + window.location.host + "/Main");
    return;
  }

  Rp = await client.send({
    prg: "Main", // Call to KtWeb:Main
    source: "Main",
    rq: "lang"
  });
  if (Rp.lang == "en") i18n.en();

  await all.request();

  Url = ui.url();
  page = dic.hasKey(Url, "0") ? Url["0"] : "cash";

  year = all.currentYear();
  myear = menu.tlink(year, year, []);

  Lopts = [
    myear,
    menu.separator(),
    menu.tlink("diary", II("Diary"), []),
    menu.separator(),
    menu.tlink("cash", II("Cash"), []),
    menu.separator(),
    menu.tlink("57202", II("Accs"), ["accs"]),
    menu.separator(),
    menu.tlink("summaries", II("Summaries"), []),
    menu.separator(),
    menu.tlink("plan", II("Plan"), [])
  ];
  menuWg = menu.mk(Lopts, [], page, false);
  if (!all.isLastYear()) myear.wg.setStyle("color", "#800000");

  body = Q("div");

  switch (page) {
    "diary": {
      ac = dic.hasKey(Url, "1") ? Url["1"] : "";
      ix = dic.hasKey(Url, "2") ? Url["2"]: "-";
      diaryPg.mk(body, ac, ix);
    }
    "cash":{
      ac = dic.hasKey(Url, "1") ? Url["1"] : "";
      ix = dic.hasKey(Url, "2") ? Url["2"]: "-";
      cashPg.mk(body, ac, ix);
    }
    "accs": {
      ac = dic.hasKey(Url, "1") ? Url["1"] : "";
      ix = dic.hasKey(Url, "2") ? Url["2"]: "-";
      accsPg.mk(body, ac, ix);
    }
    "summaries": {
      type = dic.hasKey(Url, "1") ? Url["1"] : "";
      deep = dic.hasKey(Url, "2") ? Url["2"] : "";
      summaryPg.mk(body, type, deep);
    }
    "plan": {
      ac = dic.hasKey(Url, "1") ? Url["1"] : "";
      planPg.mk(body, ac);
    }
    default: {
      if (page == all.currentYear()) {
        yearPg.mk(body);
      } else {
        ac = dic.hasKey(Url, "1") ? Url["1"] : "";
        ix = dic.hasKey(Url, "2") ? Url["2"]: "-";
        cashPg.mk(body, ac, ix);
      }
    }
  }

  wg
    .removeAll()
    .add(menuWg)
    .add(body)
  ;
};

// Main ========================================================================

wg = Q("div");

/// Load main window
load = \ -> {
  mk(wg);
};

client.init(true, "KtWeb", \ -> {
  msgWg = Q("div");
  msgPg.mk(msgWg, II("Session is expired."), true);
  Q("@body")
    .removeAll()
    .add(msgWg)
    .add(cts.foot)
  ;
});

Q("@body")
  .removeAll()
  .add(wg)
  .add(cts.foot)
  .add(ui.upTop("up"))
;

load();

try {
  Q("#autofocus").e.focus();
} catch (e) {}

