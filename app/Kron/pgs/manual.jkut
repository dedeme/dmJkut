// Copyright 23-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Manual timetable page

import "data/ann";
import "data/cts";
import "wgs/daySelector";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo> -> ()
mk = async \wg -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "Manual",
    rq: "idata"
  });
  Anns = arr.map(Rp.anns, ann.fromJs);
  Id = [-1];

  Show = [[]];

  // Control ---------------------------------------------------------------------

  // \n, <domo> -> ()
  update = async \id, txWg -> {
    tx = txWg.getValue().trim();

    err = tx == "" ? II("Command value is missing") : "";

    if (err) {
      ui.alert(err);
      return;
    }

    await client.ssend({
      prg: cts.appName,
      source: "Manual",
      rq: Id! == -1 ? "new" : "modify",
      ann: ann.toJs(ann.mk(Id!, ann.typeMANUAL, js.w(""), tx))
    });
    mk(wg);
  };

  // \n -> ()
  edit = \id -> {
    Id! = id;
    Show!();
  };

  // \ -> ()
  editCancel = \ -> {
    Id! = -1;
    Show!();
  };

  // \n -> ()
  del = async \id -> {
    if (!ui.confirm(II("Delete annotation?"))) return;

    await client.ssend({
      prg: cts.appName,
      source: "Manual",
      rq: "delete",
      id
    });
    mk(wg);
  };

  // \<domo>, n -> ()
  run = async \runSpan, id -> {
    runSpan
      .removeAll()
      .add(ui.img("wait.gif"))
    ;

    Rp = await client.send({
      prg: cts.appName,
      source: "Manual",
      rq: "run",
      id
    });

    if (Rp.error) {
      ui.alert(Rp.error);
      mk(wg);
    } else {
      runSpan
        .removeAll()
        .add(ui.link(\ev -> run(runSpan, id);)
          .add(ui.img("run")))
      ;
    }
  };

  // View ------------------------------------------------------------------------

  // \-> <domo>
  th = \ -> return Q("td").style("text-align:center");;

  // \ -> [<domo>...]
  trsNew = \ -> {
    txWg = Q("textarea")
      .att("spellcheck", false)
      .att("cols", 60)
      .att("rows", 3)
      .disabled(Id! != -1)
    ;

    return [
      Q("tr")
        .add(Q("td")
          .att("colspan", "2"))
        .add(th()
          .text(II("Command")))
        .add(Q("td")),
      Q("tr").add(Q("td").klass("line").att("colspan", "8")),
      Q("tr")
        .add(Q("td"))
        .add(Q("td")
          .add(Id! != -1
            ? ui.lightImg("add")
                .setStyle("vertical-align", "middle")
            : ui.link(\ev -> update(-1, txWg);)
              .add(ui.img("add")
                .style("vertical-align:middle"))))
        .add(Q("td")
          .add(txWg))
        .add(Q("td")),
      Q("tr").add(Q("td").att("colspan", "4").add(Q("hr")))
    ];
  };

  // \<ann> -> <domo>
  mkTr = \Ann -> {
    isSel = Ann.id == Id!;
    isNew = Id! == -1;
    txWg = Q("textarea")
      .att("spellcheck", false)
      .att("cols", 60)
      .att("rows", 3)
      .disabled(!isSel)
      .value(Ann.text)
    ;
    runSpan = Q("span");

    return Q("tr")
      .add(Q("td")
        .add(isNew
          ? ui.link(\ev -> edit(Ann.id);)
            .add(ui.img("edit"))
          : isSel
            ? ui.link(\ev -> editCancel();)
              .add(ui.img("cancel"))
            : ui.lightImg("edit")))
      .add(Q("td")
        .add(isNew
          ? ui.link(\ev -> del(Ann.id);)
            .add(ui.img("delete"))
          : isSel
            ? ui.link(\ev -> update(Ann.id, txWg);)
              .add(ui.img("enter"))
            : ui.lightImg("delete")))
      .add(Q("td")
        .add(txWg))
      .add(Q("td")
        .add(runSpan
          .removeAll()
          .add(ui.link(\ev -> run(runSpan, Ann.id);)
            .add(ui.img("run")))))
    ;
  };

  // \ -> [<domo>...]
  trs = \ -> {
    if (arr.size(Anns) == 0) {
      return [
        Q("tr")
          .add(th()
          .att("colspan", "7")
          .klass("frame")
          .text(II("Without entries")))
      ];
    }

    return arr.map(Anns, \Ann -> return mkTr(Ann););
  };

  // \ -> ()
  Show! = \ -> {
    tb = Q("table")
      .att("align", "center")
      .adds(trsNew())
      .adds(trs())
    ;

    wg
      .removeAll()
      .add(Q("div")
        .klass("head")
        .text(II("Manual")))
      .add(Q("div")
        .klass("separator"))
      .add(tb)
    ;
  };

  Show!();
};
