// Copyright 24-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Manual timetable page

import "data/ann";
import "data/cts";
import "data/fns";
import "libdm/datePicker";
import "wgs/daySelector";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo> -> ()
mk = async \wg -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "Periodic",
    rq: "idata"
  });
  Anns = arr.map(Rp.anns, ann.fromJs);
  arr.sort(Anns, \A1, A2 -> return str.toUpper(A1.text) < str.toUpper(A2.text););
  Id = [-1];

  Show = [[]];

  // Control ---------------------------------------------------------------------
  // \n, <daySelector>, <domo>, <domo>, <domo> -> ()
  update = async \id, DayDs, hWg, mWg, txWg -> {
    Days = DayDs.days;
    tx = txWg.getValue().trim();

    Err = [""];
    if (!Days) Err! = II("Week days is missing");
    if (!tx) Err! = II("Command value is missing");

    if (Err!) {
      ui.alert(Err!);
      return;
    }

    today = time.now();
    tm =  time.mk(
      time.day(today), time.month(today), time.year(today),
      math.fromStr(hWg.getValue())!, math.fromStr(mWg.getValue())!, 0
    );
    time0 = math.toInt(tm / 1000);
    time1 = arr.map(Days, \e -> return math.toInt(e););

    await client.ssend({
      prg: cts.appName,
      source: "Periodic",
      rq: Id! == -1 ? "new" : "modify",
      ann: ann.toJs(ann.mk(Id!, ann.typePERIODIC, js.w([time0, time1]), tx))
    });
    mk(wg);
  };

  // \n -> ()
  edit = \id -> {
    Id! = id;
    Show!();
  };

  // \ -> ()
  editCancel = \ -> {
    Id! = -1;
    Show!();
  };

  // \n -> ()
  del = async \id -> {
    if (!ui.confirm(II("Delete annotation?"))) return;

    await client.ssend({
      prg: cts.appName,
      source: "Periodic",
      rq: "delete",
      id
    });
    mk(wg);
  };

  // \<domo>, n -> ()
  run = async \runSpan, id -> {
    runSpan
      .removeAll()
      .add(ui.img("wait.gif"))
    ;

    Rp = await client.send({
      prg: cts.appName,
      source: "Periodic",
      rq: "run",
      id
    });

    if (Rp.error) {
      ui.alert(Rp.error);
      mk(wg);
    } else {
      runSpan
        .removeAll()
        .add(ui.link(\ev -> run(runSpan, id);)
          .add(ui.img("run")))
      ;
    }
  };

  // View ------------------------------------------------------------------------

  // \-> <domo>
  th = \ -> return Q("td").style("text-align:center");;

  // \ -> [<domo>...]
  trsNew = \ -> {
    dayWg = Q("div");
    DayDs = daySelector.mk(dayWg, [], Id! == -1);
    DayDs.show();
    hWg = ui.select(
      "h-new", arr.fromIter(iter.map([0:24], \i -> return fns.formatN00(i);))
    ).disabled(Id! != -1);
    mWg = ui.select(
      "m-new", arr.fromIter(iter.map([0:60], \i -> return fns.formatN00(i);))
    ).disabled(Id! != -1);
    txWg = Q("textarea")
      .att("spellcheck", false)
      .att("cols", 60)
      .att("rows", 3)
      .disabled(Id! != -1)
    ;

    return [
      Q("tr")
        .add(Q("td")
          .att("colspan", "2"))
        .add(th()
          .text(II("Day")))
        .add(th()
          .att("colspan", "2")
          .text(II("Hour - Minute")))
        .add(th()
          .text(II("Command")))
        .add(Q("td")),
      Q("tr").add(Q("td").klass("line").att("colspan", "8")),
      Q("tr")
        .add(Q("td"))
        .add(Q("td")
          .add(Id! != -1
            ? ui.lightImg("add")
                .setStyle("vertical-align", "middle")
            : ui.link(\ev -> update(-1, DayDs, hWg, mWg, txWg);)
              .add(ui.img("add")
                .style("vertical-align:middle"))))
        .add(Q("td")
          .add(dayWg))
        .add(Q("td")
          .add(hWg))
        .add(Q("td")
          .add(mWg))
        .add(Q("td")
          .add(txWg))
        .add(Q("td")),
      Q("tr").add(Q("td").att("colspan", "8").add(Q("hr")))
    ];
  };

  // \<ann> -> <domo>
  mkTr = \Ann -> {
    isSel = Ann.id == Id!;
    isNew = Id! == -1;

    dayWg = Q("div");
    DayDs = daySelector.mk(dayWg, ann.days(Ann), isSel);
    DayDs.show();

    hWg = ui.select(
        "h-new",
        arr.fromIter(iter.map([0:24], \i -> return
          (time.hour(ann.date(Ann)) == i ? "+" : "") + fns.formatN00(i)
        ;))
      ).disabled(!isSel)
    ;
    mWg = ui.select(
        "m-new",
        arr.fromIter(iter.map([0:60], \i -> return
          (time.minute(ann.date(Ann)) == i ? "+" : "") + fns.formatN00(i)
        ;))
      ).disabled(!isSel)
    ;


    txWg = Q("textarea")
      .att("spellcheck", false)
      .att("cols", 60)
      .att("rows", 3)
      .disabled(!isSel)
      .value(Ann.text)
    ;
    runSpan = Q("span");

    return Q("tr")
      .add(Q("td")
        .add(isNew
          ? ui.link(\ev -> edit(Ann.id);)
            .add(ui.img("edit"))
          : isSel
            ? ui.link(\ev -> editCancel();)
              .add(ui.img("cancel"))
            : ui.lightImg("edit")))
      .add(Q("td")
        .add(isNew
          ? ui.link(\ev -> del(Ann.id);)
            .add(ui.img("delete"))
          : isSel
            ? ui.link(\ev -> update(Ann.id, DayDs, hWg, mWg, txWg);)
              .add(ui.img("enter"))
            : ui.lightImg("delete")))
      .add(Q("td")
        .add(dayWg))
      .add(Q("td")
        .add(hWg))
      .add(Q("td")
        .add(mWg))
      .add(Q("td")
        .add(txWg))
      .add(Q("td")
        .add(runSpan
          .removeAll()
          .add(ui.link(\ev -> run(runSpan, Ann.id);)
            .add(ui.img("run")))))
    ;
  };

  // \ -> [<domo>...]
  trs = \ -> {
    if (arr.size(Anns) == 0) {
      return [
        Q("tr")
          .add(th()
          .att("colspan", "7")
          .klass("frame")
          .text(II("Without entries")))
      ];
    }

    return arr.map(Anns, \Ann -> return mkTr(Ann););
  };

  // \ -> ()
  Show! = \ -> {
    tb = Q("table")
      .att("align", "center")
      .adds(trsNew())
      .adds(trs())
    ;

    wg
      .removeAll()
      .add(Q("div")
        .klass("head")
        .text(II("Periodic days")))
      .add(Q("div")
        .klass("separator"))
      .add(tb)
    ;
  };

  Show!();
};
