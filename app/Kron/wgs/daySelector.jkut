// Copyright 23-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Widget to select week days

import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Constructor. <daySelector> has the following fields:
///   wg - <domo>: Container.
///   days - [n...]: Weekays selected (0 == Monday).
///   editable - b: 'true' if <daySelector> is editable.
///   show - \->(): Show <daySelector> in 'wg'.
/// Parameters:
///   wg: Container.
///   days: Weekays selected (0 == Monday)
///   editable: 'true' if <daySelector> is editable.
/// \<domo>, [n...] -> <daySelector>
mk = \wg, days, editable -> {
  // [\ ->()] | [[]]
  Show = [[]];

  // \n -> ();
  click = \n -> {
    if (arr.any(days, \d -> return d == n;)) arr.remove(days, n);
    else arr.push(days, n);
    Show!();
  };

  Show! = \-> {
    ds = II("MTWRFSU");
    tds = editable
      ? arr.fromIter(iter.map([0:7], \i -> {
          selected = arr.any(days, \d -> return d == i;);
          return Q("td")
            .klass(selected ? "frame" : "GrFrame")
            .style("font-family:monospace;cursor:pointer;" +
              (selected ? "font-weight:bold;" : "color:#a9a9a9;"))
            .on("click", \ev -> click(i);)
            .text(ds.charAt(i))
          ;
        }))
      : arr.fromIter(iter.map([0:7], \i -> {
          selected = arr.any(days, \d -> return d == i;);
          return Q("td")
            .klass("GrFrame")
            .style("font-family:monospace;"+
              (selected ? "font-weight:bold;" : "color:#a9a9a9;"))
            .text(ds.charAt(i))
          ;
        }))
    ;

    wg
      .removeAll()
      .add(Q("table")
        .klass("main")
        .add(Q("tr")
          .add(Q("td")
            .add(Q("table")
              .add(Q("tr")
                .adds(arr.take(tds, 4))))))
        .add(Q("tr")
          .add(Q("td")
            .add(Q("table")
              .att("align", "center")
              .add(Q("tr")
                .adds(arr.drop(tds, 4)))))))
    ;
  };

  return {days, editable, show: Show!};
};
