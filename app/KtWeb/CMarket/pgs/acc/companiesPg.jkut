// Copyright 14-Jun-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Comp page.

import "libdm/menu";
import "libdm/oldChart";
import "data/cts";
import "data/chart/profitsEntry";
import "wgs/msg";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Constructor
/// \<domo> -> ()
mk = async \wg -> {
  Rp = await client.send({
    prg: cts.appName,
    module: "Acc",
    source: "CompaniesPg",
    rq: "idata"
  });
  List = arr.map(
    Rp.list,
    \E -> return {nick: E[0], bought: E[1], url: E[2]};
  );

  ShowAll = [false];
  Show = [[]];

  // Control -------------------------------------------------------------------

  changeShowAll = \ -> {
    ShowAll! = !ShowAll!;
    Show!();
  };

  // View ----------------------------------------------------------------------

  // \ -> <domo>
  separator = \ -> return Q("tr")
    .add(Q("td")
      .att("colspan", 3)
      .html("<hr>"))
  ;;

  // \n, [n...], n -> s
  backColor = \prevQ, Refs, nextQ ->
    return arr.any(Refs, \ref -> return prevQ < ref & ref < nextQ;)
      ? "#fff0f0"
      : arr.any(Refs, \ref -> return prevQ > ref & ref > nextQ;)
        ? "#f0f0ff"
        : "#c9c9c9"
    ;;

  // \<domo>, s, s -> ()
  mkChart = async \wg, nick, url -> {
    wg
      .removeAll()
      .add(Q("table")
        .klass("main")
        .add(Q("tr")
          .add(Q("td")
            .style("text-align:center")
            .add(Q("img")
              .att("src", "img/wait2.gif")
              .klass("frame")))))
    ;

    Rp = await client.send({
      prg: cts.appName,
      module: "Acc",
      source: "CompaniesPg",
      rq: "nickData",
      nick
    });

    // Result of error ("" -> no error).
    error = Rp.error;
    if (error != "") {
      wg.removeAll()
        .add(Q("table")
        .klass("main")
        .add(Q("tr")
          .add(Q("td")
            .klass("frame")
            .style("width:300px")
            .text(error))))
      ;
      return;
    }

    // Price if nick is in portfolio, otherwise -1.
    price = Rp.price;
    // Profits if nick is in portfolio, otherwise 0.
    profits = Rp.profits;
    // Dates
    Dates = Rp.dates; // [s... ]
    // Closes
    Closes = Rp.closes; // [f...]
    // Referencies (one array for each investor)
    Refs = Rp.refs; // [[f...]...]

    back = backColor(
      Closes[-2:][0],
      arr.map(Refs, \Rs -> return Rs[-2:][0];),
      arr.peek(Closes)
    );

    // \ -> <domo>
    mkSmallGr = \ -> {
      Ch = oldChart.mkExample();
      Ch.ExArea.width = 300;
      Ch.ExArea.height = 150;
      Ch.ExArea.Atts.background = back;
      Ch.ExArea.Atts.Border.width = 0;
      Ch.InPadding.top = 5;
      Ch.InPadding.right = 4;
      Ch.InPadding.bottom = 4;
      Ch.InPadding.left = 50;
      Ch.ChartPadding.top = 2;
      Ch.ChartPadding.right = 2;
      Ch.ChartPadding.bottom = 2;
      Ch.ChartPadding.left = 2;
      Ch.Labels.show = false;

      Lbs = [];
      Vals = [[], [], []]; // Removed investors 1 and 2.
      Dates2 = Dates[-250:];
      Closes2 = Closes[-250:];
      Refs2 = Refs[0][-250:];
      for (i = 0:arr.size(Dates2)) {
        cl = Closes2[i];
        rf = Refs2[i];
        arr.push(Lbs, time.toIso(time.fromStr(Dates2[i])!));
        arr.push(Vals[0], [cl]);
        arr.push(Vals[1], cl > rf ? [rf] : []);
        arr.push(Vals[2], cl < rf ? [rf] : []);
      }
      Atts = [
        oldChart.mkLine(1.2, "#414141", false),
        oldChart.mkLine(1.2, "#aa4100", false),
        oldChart.mkLine(1.2, "#0041aa", false)
      ];
      Data = oldChart.mkData(Lbs, Vals, Atts);
      Data.Labels = Lbs;
      Data.SetValues = [Vals[0], Vals[1], Vals[2]];
      if (price >= 0)
        Data.UnarySets = [oldChart.mkUnarySet(
            II("Price"),
            price,
            oldChart.mkLine(1.2, "#c9c9c9", false)
          )];
      Data.SetAtts = Atts;
      Data.maxMinRound = \mx, mn -> {
        if (mn > 10) {
          Data.round = 1;
          return -2;
        } else if (mn > 1) {
          Data.round = 2;
          return -3;
        } else {
          Data.round = 3;
          return -4;
        }
      };
      Data.drawGrid = \lb, i -> return false;;
      Data.drawLabel = Data.drawGrid;

      return Q("table")
        .add(Q("tr")
          .add(Q("td")
            .klass("frame0")
            .style("background-color:" + back)
            .add(oldChart.mkWg(Ch, Data))))
      ;
    };

    // \ -> <domo>
    mkBigGr = \ -> {
      Ch = oldChart.mkExample();
      Ch.ExArea.width = 600;
      Ch.ExArea.height = 300;
      Ch.ExArea.Atts.background = back;
      Ch.ExArea.Atts.Border.width = 0;
      Ch.ExArea.Atts.background = back;
      Ch.InPadding.top = 10;
      Ch.InPadding.right = 5;
      Ch.InPadding.bottom = 20;
      Ch.InPadding.left = 80;
      Ch.ChartPadding.top = 2;
      Ch.ChartPadding.right = 4;
      Ch.ChartPadding.bottom = 2;
      Ch.ChartPadding.left = 2;
      Ch.Labels.onPopup = true;

      mk0 = \chartDiv, zoomDiv, zoom -> {
        Lbs = [];
        Vals = [[], [], []]; // Removed investors 1 and 2.
        Dates2 = zoom ? Dates[-30:] : Dates;
        Closes2 = zoom ? Closes[-30:] : Closes;
        Refs2 = zoom ? Refs[0][-30:] : Refs[0];

        for (i = 0:arr.size(Dates2)) {
          cl = Closes2[i];
          rf = Refs2[i];
          arr.push(Lbs, time.toIso(time.fromStr(Dates2[i])!));
          arr.push(Vals[0], [cl]);
          arr.push(Vals[1], cl > rf ? [rf] : []);
          arr.push(Vals[2], cl < rf ? [rf] : []);
        }
        wln = zoom ? 1.8 : 1;
        Atts = [
          oldChart.mkLine(wln, "#414141", false),
          oldChart.mkLine(wln, "#aa4100", false),
          oldChart.mkLine(wln, "#0041aa", false)
        ];
        Data = oldChart.mkData(Lbs, Vals, Atts);
        Data.Labels = Lbs;
        Data.SetValues = [Vals[0], Vals[1], Vals[2]];
        if (price >= 0 & !zoom)
          Data.UnarySets = [oldChart.mkUnarySet(
              II("Price"),
              price,
              oldChart.mkLine(1.2, "#c9c9c9", false)
            )];
        Data.SetAtts = Atts;
        Data.maxMinRound = \mx, mn -> {
          if (mn > 10) {
            Data.round = 1;
            return -2;
          } else if (mn > 1) {
            Data.round = 2;
            return -3;
          } else {
            Data.round = 3;
            return -4;
          }
        };
        Data.drawGrid = \lb, i -> {
          if (i == 0) return false;
          return zoom
            ? i % 2 == 0
            : Lbs[i - 1][3:5] != lb[3:5] &
              switch (lb[3:5]) {"03", "06", "09", "12": true; default: false;}
          ;
        };
        Data.drawLabel = Data.drawGrid;
        Data.mapLabel = \lb, i -> return zoom ? lb[:2] : lb[3:5];;

        chartDiv
          .removeAll()
          .add(oldChart.mkWg(Ch, Data))
        ;

        zoomDiv
          .removeAll()
          .add(ui.link(\e -> mk0(chartDiv, zoomDiv, !zoom);)
              .klass("frame")
              .add(ui.img(zoom ? "rk-new" : "minus2")
                  .style("vertical-align:top")))
        ;
      };

      chartDiv = Q("div");
      zoomDiv = Q("div");
      mk0(chartDiv, zoomDiv, true);
      return Q("table")
        .add(Q("tr")
          .add(Q("td")
            .style("text-align:center")
            .text(nick))
          .add(Q("td")))
        .add(Q("tr")
          .add(Q("td")
            .klass("frame0")
            .style("background-color:" + back)
            .add(chartDiv))
          .add(Q("td")
            .style("width:80px;vertical-align:middle;")
            .add(zoomDiv)))


      ;
    };

    wg
      .removeAll()
      .add(Q("table")
        .klass("main")
        .add(Q("tr")
          .add(Q("td")
            .style("text-align:left;width:40%")
            .add(Q("a")
              .klass("link")
              .att("href", url)
              .text(nick)))
          .add(Q("td")
            .style("text-align:right;width:40%")
            .add(Q("span")
              .html(math.toIso(profits, 2) + "&nbsp;&nbsp;"))
            .add(ui.img(
              profits > 0 ? "profits" : profits < 0 ? "losses" : "noresult"
            )
              .style("vertical-align:middle"))))
        .add(Q("tr")
          .add(Q("td")
            .att("colspan", 2)
            .add(mkSmallGr()
              .setStyle("cursor", "pointer")
              .on("click", \e -> msg.showWg(mkBigGr(), \->{});)))))
    ;
  };

  // \ ->()
  Show! = \ -> {
    Ls = ShowAll!
      ? List
      : arr.filter(List, \E -> return E.bought;)
    ;
    arr.sort(Ls, \E1, E2 -> return E1.nick < E2.nick;);

    chs = Q("table")
      .att("align", "center")
      .klass("frame")
    ;

    n = arr.size(Ls);
    Tr = [Q("tr")];
    for (i = 0:n) {
      chart = Q("div");
      mkChart(chart, Ls[i].nick, Ls[i].url);
      switch (i % 3) {
        0: {
          chs.add(separator());
          Tr! = Q("tr");
          Tr!.add(Q("td").add(chart));
        }
        2: {
          Tr!.add(Q("td").add(chart));
          chs.add(Tr!);
        }
        default:
          Tr!.add(Q("td").add(chart));
      }
    }

    switch (n % 3) {
      1: chs.add(Tr!.add(Q("td")).add(Q("td")));
      2: chs.add(Tr!.add(Q("td")));
    }
    chs.add(separator());

    wg
      .removeAll()
      .add(Q("div")
        .style("text-align:center")
        .add(ui.link(\e -> changeShowAll();)
          .klass("link")
          .html(ShowAll!
              ? II("Portfolio")
              : II("All Companies")
            )))
      .add(chs)
    ;
  };

  Show!();

};
