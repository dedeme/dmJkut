// Copyright 26-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Home page.

import "libdm/log";
import "data/cts";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Constructor.
/// \<domo> -> ()
mk = async \wg -> {
  Rp = await client.send({
    prg: cts.appName,
    module: "Home",
    source: "Home",
    rq: "idata"
  });

  isActive = Rp.isActive;

  logDiv = Q("div");

  // Control -------------------------------------------------------------------

  // \ -> ()
  activate = async \ev -> {
    logDiv
      .removeAll()
      .add(Q("table")
        .att("align", "center")
        .add(Q("tr")
          .add(Q("td")
            .add(ui.img("wait.gif")))))
    ;
    await client.send({
      prg: cts.appName,
      module: "Home",
      source: "Home",
      rq: "activate"
    });
    window.location.reload(true);
  };

  // View ----------------------------------------------------------------------

  // \(\[<logRow>...]->()) -> ()
  load = async \fn -> {
    Rp = await client.send({
      prg: cts.appName,
      module: "Home",
      source: "Home",
      rq: "getLog"
    });
    fn(arr.map(Rp.log, log.logRowFromJs));
  };

  // \(\->()) -> ()
  reset = async \fn -> {
    await client.send({
      prg: cts.appName,
      module: "Home",
      source: "Home",
      rq: "resetLog"
    });
    fn();
  };

  // \s -> s
  tlt = \tx -> return switch (tx) {
    "All log entries will be deleted.\nContinue?":
      II("All log entries will be deleted.\nContinue?");
    "2 Days": II("2 Days");
    "All": II("All");
    "Reload": II("Reload");
    "Delete": II("Delete");
    "Errors": II("Errors");
    "Log": II("Log");
    default: tx;
  };;

  log.mk(logDiv, load, reset, tlt, true, 100, 25);

  wg
    .removeAll()
    .add(Q("div")
      .klass("head")
      .text(II("Server")))
    .add(Q("table")
      .att("align", "center")
      .add(Q("tr")
        .add(Q("td")
          .klass("frame")
          .att("colspan", 3)
          .style("text-align: center")
          .text(isActive
            ? II("Active")
            : II("Stopped"))))
      .add(Q("tr")
        .add(Q("td")
          .style("width:100px;text-align:center")
          .add(isActive
            ? Q("span")
              .text("* * *")
            : ui.link(activate)
              .klass("link")
              .text(II("Activate"))))))
    .add(Q("hr"))
    .add(logDiv)
  ;
};
