// Copyright 26-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Program starting.

import "libdm/menu";
import "cts";
import "pgs/msgPg";
import "pgs/billsPg";
import "pgs/staysPg";
import "pgs/summaryPg";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// \<domo> -> ()
mk = async \wg -> {
  ok = await client.connect();
  if (!ok) {
    ui.alert(II("Session is closed.\nAuthenticating from Main."));
    window.location.assign("http://" + window.location.host + "/Main");
    return;
  }

  // Sessions control is in "KtWeb:Main" !!!
  , lang : await client.send({
    prg: "Main", // Call to KtWeb:Main
    source: "Main",
    rq: "lang"
  });
  if (lang == "en") i18n.en();

  :arr Url = ui.url();
  page = Url.size() > 0 ? Url[0] : "summary";
  menuWg = menu.mk(
    [ menu.tlink("summary", II("Summary")),
      menu.separator(),
      menu.tlink("bills", II("Bills")),
      menu.separator(),
      menu.tlink("stays", II("Stays"))
    ],
    [],
    page
  );

  body = Q("div");

  switch (page) {
    "bills": billsPg.mk(body);
    "stays": staysPg.mk(body);
    default: summaryPg.mk(body);
  }

  wg
    .removeAll()
    .add(menuWg)
    .add(body)
  ;
};

// Main ========================================================================

wg = Q("div");

/// Load main window
load = \ -> {
  mk(wg);
};

// Client must be initilized with "KtWeb" !!!
client.init(true, "KtWeb", \isExpired -> {
  message = isExpired
    ? II("Session is expired.")
    : II("Data base is out of date.")
  ;
  msgWg = Q("div");
  msgPg.mk(msgWg, message, true);
  Q("@body")
    .removeAll()
    .add(msgWg)
    .add(cts.foot)
  ;
});

Q("@body")
  .removeAll()
  .add(wg)
  .add(cts.foot)
;

load();
