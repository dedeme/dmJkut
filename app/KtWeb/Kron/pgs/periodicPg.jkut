// Copyright 27-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Manual timetable page

import "data/ann";
import "cts";
import "fns";
import "global";
import "libdm/datePicker";
import "wgs/daySelector";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo> -> ()
mk = async \wg -> {
  // s, [<ann>.](JS)
  dbKey, Anns : await client.send({
    prg: cts.appName,
    source: "PeriodicPg",
    rq: "idata"
  });
  global.dbKeyV! = dbKey;

  //[<ann>.]
  :arr As = arr.map(Anns, ann.fromJs);
  As.sort(
    \:ann a1, :ann a2 -> return str.toUpper(a1.text) < str.toUpper(a2.text);
  );
  idV = [-1];

  showV = [[]];

  // Control ---------------------------------------------------------------------
  // \n, <daySelector>, <domo>, <domo>, <domo> -> ()
  update = async \id, DayDs, hWg, mWg, txWg -> {
    :arr Days = DayDs.Days;
    tx = txWg.getValue().trim();

    errV = [""];
    if (!Days) errV! = II("Week days is missing");
    if (tx == "") errV! = II("Command value is missing");

    if (errV! != "") {
      ui.alert(errV!);
      return;
    }

    today = time.now();
    tm =  time.mk(
      today.day(), today.month(), today.year(),
      math.fromStr(hWg.getValue())!, math.fromStr(mWg.getValue())!, 0
    );
    time0 = math.toInt(tm / 1000);
    time1 = Days.map(\e -> return math.toInt(e););

    await client.send({
      prg: cts.appName,
      source: "PeriodicPg",
      rq: idV! == -1 ? "new" : "modify",
      dbKey: global.dbKeyV!,
      annotation: ann.toJs(
          ann.mk(idV!, ann.typePERIODIC, js.w([time0, time1]), tx)
        )
    });
    mk(wg);
  };

  // \n -> ()
  edit = \id -> {
    idV! = id;
    showV!();
  };

  // \ -> ()
  editCancel = \ -> {
    idV! = -1;
    showV!();
  };

  // \n -> ()
  del = async \id -> {
    if (!ui.confirm(II("Delete annotation?"))) return;

    await client.send({
      prg: cts.appName,
      source: "PeriodicPg",
      rq: "delete",
      dbKey: global.dbKeyV!,
      id
    });
    mk(wg);
  };

  // \<domo>, n -> ()
  run = async \runSpan, id -> {
    runSpan
      .removeAll()
      .add(ui.img("wait.gif"))
    ;

    // s
    , error : await client.send({
      prg: cts.appName,
      source: "PeriodicPg",
      rq: "run",
      id
    });

    if (error != "") {
      ui.alert(error);
      mk(wg);
    } else {
      runSpan
        .removeAll()
        .add(ui.link(\ev -> run(runSpan, id);)
          .add(ui.img("run")))
      ;
    }
  };

  // View ------------------------------------------------------------------------

  // \-> <domo>
  th = \ -> return Q("td").style("text-align:center");;

  // \ -> [<domo>...]
  trsNew = \ -> {
    dayWg = Q("div");
    :: DayDs = daySelector.mk(dayWg, [], idV! == -1);
    DayDs.show();
    hWg = ui.select(
      "h-new", arr.fromIter(iter.map([0:24], \i -> return fns.formatN00(i);))
    ).disabled(idV! != -1);
    mWg = ui.select(
      "m-new", arr.fromIter(iter.map([0:60], \i -> return fns.formatN00(i);))
    ).disabled(idV! != -1);
    txWg = Q("textarea")
      .att("spellcheck", false)
      .att("cols", 60)
      .att("rows", 3)
      .disabled(idV! != -1)
    ;

    return [
      Q("tr")
        .add(Q("td")
          .att("colspan", "2"))
        .add(th()
          .text(II("Day")))
        .add(th()
          .att("colspan", "2")
          .text(II("Hour - Minute")))
        .add(th()
          .text(II("Command")))
        .add(Q("td")),
      Q("tr").add(Q("td").klass("line").att("colspan", "8")),
      Q("tr")
        .add(Q("td"))
        .add(Q("td")
          .add(idV! != -1
            ? ui.lightImg("add")
                .setStyle("vertical-align", "middle")
            : ui.link(\ev -> update(-1, DayDs, hWg, mWg, txWg);)
              .add(ui.img("add")
                .style("vertical-align:middle"))))
        .add(Q("td")
          .add(dayWg))
        .add(Q("td")
          .add(hWg))
        .add(Q("td")
          .add(mWg))
        .add(Q("td")
          .add(txWg))
        .add(Q("td")),
      Q("tr").add(Q("td").att("colspan", "8").add(Q("hr")))
    ];
  };

  // \<ann> -> <domo>
  mkTr = \:ann a -> {
    isSel = a.id == idV!;
    isNew = idV! == -1;

    dayWg = Q("div");
    :: DayDs = daySelector.mk(dayWg, a.days(), isSel);
    DayDs.show();

    hWg = ui.select(
        "h-new",
        arr.fromIter(iter.map([0:24], \i -> return
          (time.hour(a.date()) == i ? "+" : "") + fns.formatN00(i)
        ;))
      ).disabled(!isSel)
    ;
    mWg = ui.select(
        "m-new",
        arr.fromIter(iter.map([0:60], \i -> return
          (time.minute(a.date()) == i ? "+" : "") + fns.formatN00(i)
        ;))
      ).disabled(!isSel)
    ;


    txWg = Q("textarea")
      .att("spellcheck", false)
      .att("cols", 60)
      .att("rows", 3)
      .disabled(!isSel)
      .value(a.text)
    ;
    runSpan = Q("span");

    return Q("tr")
      .add(Q("td")
        .add(isNew
          ? ui.link(\ev -> edit(a.id);)
            .add(ui.img("edit"))
          : isSel
            ? ui.link(\ev -> editCancel();)
              .add(ui.img("cancel"))
            : ui.lightImg("edit")))
      .add(Q("td")
        .add(isNew
          ? ui.link(\ev -> del(a.id);)
            .add(ui.img("delete"))
          : isSel
            ? ui.link(\ev -> update(a.id, DayDs, hWg, mWg, txWg);)
              .add(ui.img("enter"))
            : ui.lightImg("delete")))
      .add(Q("td")
        .add(dayWg))
      .add(Q("td")
        .add(hWg))
      .add(Q("td")
        .add(mWg))
      .add(Q("td")
        .add(txWg))
      .add(Q("td")
        .add(runSpan
          .removeAll()
          .add(ui.link(\ev -> run(runSpan, a.id);)
            .add(ui.img("run")))))
    ;
  };

  // \ -> [<domo>...]
  trs = \ -> {
    if (As.size() == 0) {
      return [
        Q("tr")
          .add(th()
          .att("colspan", "7")
          .klass("frame")
          .text(II("Without entries")))
      ];
    }

    return arr.map(As, \a -> return mkTr(a););
  };

  // \ -> ()
  showV! = \ -> {
    tb = Q("table")
      .att("align", "center")
      .adds(trsNew())
      .adds(trs())
    ;

    wg
      .removeAll()
      .add(Q("div")
        .klass("head")
        .text(II("Periodic days")))
      .add(Q("div")
        .klass("separator"))
      .add(tb)
    ;
  };

  showV!();
};
