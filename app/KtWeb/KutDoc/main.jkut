// Copyright 03-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Program starting.

import "cts";
import "libdm/menu";
import "data/dpath";
import "data/conf";
import "pgs/msgPg";
import "pgs/pathsPg";
import "pgs/indexPg";
import "pgs/modulePg";
import "pgs/codePg";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// \<domo> -> ()
mk = async \wg -> {
  ok = await client.connect();
  if (!ok) {
    ui.alert(II("KtWeb session is closed.\nAuthenticating from KtWeb:Main."));
    window.location.assign("http://" + window.location.host + "/Main");
  } else {
    rp = await client.send({
      prg: "Main", // Call to KtWeb:Main
      source: "Main",
      rq: "lang"
    });
    if (rp.lang == "en") i18n.en();

    rp2 = await client.send({
      prg: "KutDoc",
      source: "Main",
      rq: "idata"
    });
    Cf = conf.fromJs(rp2.conf);
    Paths = arr.map(rp2.paths, dpath.fromJs);
    arr.sort(Paths, \p1, p2 -> return str.less(p1.id, p2.id););

    Url = ui.url();
    page = arr.size(Url) > 0 ? Url["0"] : Cf.path;

    Parts = page.split("@");
    pack = page == "@" ? page : Parts[0];
    PkPath = Parts.length > 1 & pack != "@" ? [Parts[1]] : [];

    anchorOp = arr.size(Url) > 1 ? [Url[1]] : [];

    client.send({
      prg: "KutDoc",
      source: "Main",
      rq: "savePath",
      path: !PkPath ? pack : pack + "@" + PkPath!
    });

    Lopts = [menu.ilink("@", "asterisk", [])];

    for (p = Paths) {
      if (p.isValid & p.isShown) {
        arr.push(Lopts, menu.separator());
        arr.push(Lopts, menu.tlink(p.id, p.id, []));
      }
    }
    menuWg = menu.mk(Lopts, [], pack, false);

    body = Q("div");
    if (pack == "@") pathsPg.mk(body, Cf, Paths);
    else if (!PkPath) indexPg.mk(body, pack);
    else if (!!anchorOp) codePg.mk(body, pack, PkPath!, anchorOp!);
    else modulePg.mk(body, pack, PkPath!);

    wg
      .removeAll()
      .add(menuWg)
      .add(body)
    ;
  }
};

wg = Q("div");

/// Load main window
load = async \ -> {
  await mk(wg);

  Fc = ui.qOp("#autofocus");
  if (!!Fc) Fc!.e.focus();
};

// Main ------------------------------------------------------------------------

client.init(true, "KtWeb", \ -> {
  wg = Q("div");
  msgPg.mk(wg, II("Session is expired."), true);
  Q("@body")
    .removeAll()
    .add(wg)
    .add(cts.foot)
  ;
});

Q("@body")
  .removeAll()
  .add(wg)
  .add(cts.foot)
  .add(ui.upTop("up"))
;

load();
