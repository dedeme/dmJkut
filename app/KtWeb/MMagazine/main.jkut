// Copyright 03-May-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Application entry.

import "cts";
import "pgs/msgPg";
import "pgs/market";
import "pgs/main/homePg";
import "wgs/dmenu";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// \<domo> -> ()
mk = async \wg -> {
  ok = await client.connect();
  if (!ok) {
    ui.alert(II("Session is closed.\nAuthenticating from Main."));
    window.location.assign("http://" + window.location.host + "/Main");
    return;
  }

  // Sessions control is in "KtWeb:Main" !!!
  , lang : await client.send({
    prg: "Main", // Call to KtWeb:Main
    source: "Main",
    rq: "lang"
  });
  if (lang == "en") i18n.en();

  search = window.location.search;
  :arr LcPath = search == ""
    ? []
    : search[1:].split("&")
  ;
  if (!LcPath) LcPath.push("home");

  target = switch (LcPath[0]) {
      "market": LcPath[0];
      default: "home";
    };
  arr.shift(LcPath);

  menuDiv = Q("div");
  bodyDiv = Q("div");
  menu = dmenu.mk(menuDiv, target);

  switch (target) {
    "market": market.process(bodyDiv, menu, LcPath);
    default: homePg.mk(bodyDiv);
  }

  wg
    .removeAll()
    .add(menuDiv)
    .add(bodyDiv)
  ;
};

// Main ========================================================================

wg = Q("div");

/// Load main window
/// \ -> ()
load = \ -> {
  mk(wg);
};

// Client must be initilized with "KtWeb" !!!
client.init(true, "KtWeb", \isExpired -> {
  message = isExpired
    ? II("Session is expired.")
    : II("Data base is out of date.")
  ;
  msgWg = Q("div");
  msgPg.mk(msgWg, message, true);
  Q("@body")
    .removeAll()
    .add(msgWg)
    .add(cts.foot)
  ;
});

Q("@body")
  .removeAll()
  .add(wg)
  .add(cts.foot)
;

load();
