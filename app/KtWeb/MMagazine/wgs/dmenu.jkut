// Copyright 26-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Double menu widget.

import "libdm/menu";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// Main menu constructor.
//    selected: Selected option.
// \s -> <domo>
mkUpMenu= \selected -> {
  Lopts = [
    menu.tlink("home", II("Home")),
    menu.separator(),
    menu.tlink("market", II("Market"))
  ];

  return menu.mk(Lopts, [], selected);
};

/// Constructor. Contains the following fields:
/// Parameters:
///   wg: Container.
///   selected: Selected option.
/// \<domo>, s -> <dmenu>
mk = \wg, selected -> {
  upDiv = Q("div").style("padding:0px");
  upMenu = mkUpMenu(selected);
  downDiv = Q("div");
  hiddenV = [false];

  showV = [[]];

  // Control -------------------------------------------------------------------

  // \<event> -> ()
  change = \ev -> {
    hiddenV! = !hiddenV!;
    upDiv.removeAll().style("padding:0px");
    if (!hiddenV!) upDiv.add(upMenu);
  };

  // Sets down menu.
  // \<domo> -> ()
  setDownMenu = \menuWg -> {
    downDiv
      .removeAll()
      .add(menuWg)
    ;
    hiddenV! = true;
    upDiv.removeAll().style("padding:0px");
  };

  // View ----------------------------------------------------------------------

  // \->()
  showV! = \ -> {
    wg
      .removeAll()
      .add(upDiv
        .removeAll()
        .style("padding:0px")
        .add(upMenu))
      .add(downDiv)
    ;
  };

  showV!();

  return {setDownMenu, change};
};

/// Sets down menu.
/// \<dmenu>, <domo> -> ()
setDownMenu = \odmenu, downMenu -> return odmenu.setDownMenu(downMenu);;

/// Returns a button to hidde-show the up menu.
/// \<dmenu> -> <menuEntry>
mkHiddenButton = \odmenu -> return menu.mkEntry(
    [],
    ui.link(odmenu.change)
      .add(ui.img("menu")
        .style("vertical-align:middle"))
  );;
