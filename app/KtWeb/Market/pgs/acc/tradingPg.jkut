// Copyright 15-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Trading page.

import "libdm/menu";
import "libdm/modalBox";
import "cts";
import "data/broker";
import "data/invOperation";
import "wgs/msg";
import "global";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// \n -> <domo>
invTd = \inv ->
  return Q("td")
    .klass("borderWhite")
    .text("" + inv)
  ;;

// \d -> <domo>
ciaTd = \nick ->
  return Q("td")
    .klass("borderWhite")
    .style("text-align:left")
    .text(nick)
  ;;

// \n -> <domo>
stocksTd = \stocks ->
  return Q("td")
    .klass("borderWhite")
    .style("text-align:right")
    .text(math.toIso(stocks, 0))
  ;;

// \n -> <domo>
quoteTd = \q ->
  return Q("td")
    .klass("borderWhite")
    .style("text-align:right")
    .text(math.toIso(q, 4))
  ;;

// \n -> <domo>
moneyTd = \q ->
  return Q("td")
    .klass("borderWhite")
    .style("text-align:right")
    .text(math.toIso(q, 2))
  ;;

// Main ------------------------------------------------------------------------

/// Constructor
/// \<domo> -> ()
mk = async \wg -> {
  dbKey, ok,
  :arr InvOperations, // [<invOperation>.]
  :dic Portfolio, // {[n, n].} :: {nick -> [stocks, price].}
  :dic Closes, // {f.} :: {nick -> close}
  :dic Rebuys // {s.} :: {nick -> date}
  : await client.send({
    prg: cts.appName,
    module: "Acc",
    source: "TradingPg",
    rq: "idata"
  });
  global.dbKeyV! = dbKey;

  if (!ok) {
    msg.error(cts.failMsg, \->{});
    return;
  }

  msgWait = Q("div");
  bentry = ui.field("pentry")
    .style("width:80px")
    .value(math.toIso(cts.bet, 0));
  ui.changePoint(bentry);
  pentry = ui.field("calcBuyBt")
    .style("width:80px")
    .att("id", "pentry");
  ui.changePoint(pentry);
  price2 = Q("div").klass("frame").style("text-align:right");
  result = Q("div").klass("frame").style("text-align:right");
  result2 = Q("div").klass("frame").style("text-align:right");

  sentry = ui.field("calcSellBt")
    .style("width:80px")
    .att("id", "sentry");
  ui.changePoint(sentry);
  sprice2 = Q("div").klass("frame").style("text-align:right");

  // Control -------------------------------------------------------------------

  // \ -> ()
  update = async \ -> {
    box = modalBox.mk(
      Q("div")
        .add(Q("div")
          .style("text-align:center")
          .add(ui.img("wait2.gif").klass("frame"))),
      false
    );
    msgWait.add(box.mkWg());
    modalBox.show(box, true);
    , dbKey : await client.send({
      prg: cts.appName,
      module: "Acc",
      source: "TradingPg",
      rq: "update",
      dbKey: global.dbKeyV!
    });
    global.dbKeyV! = dbKey;

    modalBox.show(box, false);
    mk(wg);
  };

  // \-> ()
  calculateBuy = \ -> {
    bs = bentry.getValue();
    ps = pentry.getValue();
    bOp = math.fromIso(bs);
    if (!bOp) {
      ui.alert(i18n.fmt(II("'%0' is not a valid number."), [bs]));
      return;
    }
    b = bOp!;
    b2 = b + b - broker.buy(1, b);

    pOp = math.fromIso(ps);
    if (!pOp) {
      ui.alert(i18n.fmt(II("'%0' is not a valid number."), [ps]));
      return;
    }
    p = pOp!;
    if (p == 0) {
      ui.alert(II("Price is 0"));
      return;
    }

    rs = math.toInt(b2 / p);
    p2 = p * 0.99;
    rs2 = math.toInt(b2 / p2);

    price2.text(math.toIso(p2, 2));
    result.text(math.toIso(rs, 0));
    result2.text(math.toIso(rs2, 0));
  };

  // \ -> ()
  calculateSell = \ -> {
    ps = sentry.getValue();
    pOp = math.fromIso(ps);
    if (!pOp) {
      ui.alert(i18n.fmt(II("'%0' is not a valid number."), [ps]));
      return;
    }
    sprice2.text(math.toIso(pOp! * 1.01, 2));
  };

  // View ----------------------------------------------------------------------

  // \-> <domo>
  buyWg = \ ->
    return Q("table")
      .klass("frame4")
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .add(Q("div")
            .klass("head")
            .text(II("Buy")))))
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .add(Q("hr"))))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:right")
          .add(Q("div")
            .html("<b>" + II("Invest") + ":</b>")))
        .add(Q("td")
          .add(bentry)))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:right")
          .add(Q("div")
            .html("<b>" + II("Price") + ":</b>")))
        .add(Q("td")
          .add(pentry)))
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .style("text-align:center")
          .add(Q("button")
            .att("id", "calcBuyBt")
            .text(II("Calculate"))
            .on("click", \e -> calculateBuy();))))
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .add(Q("hr"))))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:right")
          .add(Q("div")
            .html("<b>" + II("Stocks") + ":</b>")))
        .add(Q("td")
          .add(result)))
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .add(Q("hr"))))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:right;white-space:nowrap")
          .add(Q("div")
            .html("<b>" + II("Price") + " + :</b>")))
        .add(Q("td")
          .add(price2)))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:right;white-space:nowrap")
          .add(Q("div")
            .html("<b>" + II("Stocks") + " + :</b>")))
        .add(Q("td")
          .add(result2)))
  ;;

  // \ -> <domo>
  sellWg = \ ->
    return Q("table")
      .klass("frame3")
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .add(Q("div")
            .klass("head")
            .text(II("Sell")))))
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .add(Q("hr"))))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:right")
          .add(Q("div")
            .html("<b>" + II("Price") + ":</b>")))
        .add(Q("td")
          .add(sentry)))
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .style("text-align:center")
          .add(Q("button")
            .att("id", "calcSellBt")
            .text(II("Calculate"))
            .on("click", \e -> calculateSell();))))
      .add(Q("tr")
        .add(Q("td")
          .att("colspan", "2")
          .add(Q("hr"))))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:right;white-space:nowrap")
          .add(Q("div")
            .html("<b>" + II("Price") + " + :</b>")))
        .add(Q("td")
          .add(sprice2)))
    ;;

  :arr RebuysArr = dic.toArr(Rebuys);
  RebuysArr.sort(\Tp1, Tp2 ->
    return Tp1[1] == Tp2[1] ? Tp1[0] < Tp2[0] : Tp1[1] < Tp2[1];);
  RebuyTrs = !RebuysArr
    ? [ Q("tr")
        .add(Q("td")
          .att("colspan", 2)
          .klass("borderWhite")
          .html(II("Without operations")))
      ]
    : \ -> {
      Trs = [];
      iV =[0];
      trV = [Q("tr")];
      while (iV! < RebuysArr.size()) {
        Tp = RebuysArr[iV!];
        trV!
          .add(ciaTd(Tp[0]))
          .add(ciaTd(
            i18n.getLang() == "es"
              ? time.toIso(time.fromStr(Tp[1])!)
              : time.toEn(time.fromStr(Tp[1])!)))
        ;
        if (iV! % 3 != 2)
          trV!.add(Q("td").klass("separator"));
        iV! += 1;
        if (iV! % 3 == 0) {
          Trs.push(trV!);
          trV! = Q("tr");
        }
      }
      if (iV! % 3 != 0) Trs.push(trV!);
      return Trs;
    }()
  ;

  InvOperations.sort(
    \:invOperation o1, :invOperation o2 -> return o1.nick < o2.nick;
  );
  :arr BuyOps = InvOperations.filter(
    \:invOperation o -> return o.stocks <= 0;
  );
  BuyTrs = !BuyOps
    ? [ Q("tr")
        .add(Q("td")
          .klass("borderWhite")
          .html(II("Without operations")))
      ]
    : BuyOps.map(
        \:invOperation o -> return Q("tr")
          .add(ciaTd(o.nick).setStyle(
              "text-decoration",
              RebuysArr.any(\Tp -> return Tp[0] == o.nick;)
              ? "line-through"
              : ""
            ))
      ;)
  ;

  SellOps = InvOperations.filter(\:invOperation o ->
    return o.stocks > 0 & dic.hasKey(Portfolio, o.nick);
  );
  SellTrs = !SellOps
    ? [ Q("tr")
        .add(Q("td")
          .att("colspan", 5)
          .klass("borderWhite")
          .html(II("Without operations")))
      ]
    : SellOps.map(
        \:invOperation o -> {
          pr = Portfolio[o.nick][1];
          cl = Closes[o.nick];
          dif = (cl - pr) * 100 / pr;
          color = dif >= 0 ? "#d0d9f0" : dif >= -5 ? "#f0f0d9" : "#ffffff";
          return Q("tr")
            .add(ciaTd(o.nick).setStyle("background", color))
            .add(stocksTd(o.stocks).setStyle("background", color))
            .add(quoteTd(pr).setStyle("background", color))
            .add(quoteTd(cl).setStyle("background", color))
            .add(quoteTd(dif).setStyle("background", color))
          ;
      })
  ;

  wg
    .removeAll()
    .add(msgWait)
    .add(Q("table")
      .klass("main")
      .add(Q("tr")
        .add(Q("td")
          .style("vertical-align:top;width:5px")
          .add(buyWg()))
        .add(Q("td")
          .style("text-align:center")
            .add(Q("table")
              .att("align", "center")
              .add(Q("tr")
                .add(Q("td")
                  .add(ui.link(\e ->update();)
                    .klass("link")
                    .text(II("Update")))))
              .add(Q("tr")
                .add(Q("td")
                  .klass("head")
                  .html("&nbsp;"))))
            .add(Q("table")
              .klass("frame")
              .att("align", "center")
              .add(Q("tr")
                .add(Q("td")
                  .add(Q("div")
                    .klass("head")
                    .html(II("Rebuys")))
                  .add(Q("table")
                    .klass("frame2")
                    .style("border-collapse : collapse;")
                    .add(Q("tr")
                      .adds(iter.reduce(
                          [0:(arr.size(RebuysArr) > 2 ? 2 : arr.size(RebuysArr) -1)],
                          [ Q("td").klass("head").html(II("Co.")),
                            Q("td").klass("head").html(II("Date")),
                            Q("td").klass("separator")
                          ],
                          \R, i -> {
                            arr.push(R, Q("td").klass("head").html(II("Co.")));
                            arr.push(R, Q("td").klass("head").html(II("Date")));
                            if (i % 3 != 1)
                              arr.push(R, Q("td").klass("separator"));
                            return R;
                          }
                        )))
                    .adds(RebuyTrs)))))
          .add(Q("div")
            .klass("head")
            .html("&nbsp;"))
          .add(Q("div")
            .klass("head")
            .html(II("Buys")))
          .add(Q("table")
            .att("align", "center")
            .klass("buys")
            .add(Q("tr")
              .add(Q("td").klass("head").html(II("Co."))))
            .adds(BuyTrs))
          .add(Q("div")
            .klass("head")
            .html(II("Sells")))
          .add(Q("table")
            .att("align", "center")
            .klass("sells")
            .add(Q("tr")
              .add(Q("td").klass("head").html(II("Co.")))
              .add(Q("td").klass("head").html(II("Stocks")))
              .add(Q("td").klass("head").html(II("Price")))
              .add(Q("td").klass("head").html(II("Quote")))
              .add(Q("td").klass("head").html("%&Delta;"))
              )
            .adds(SellTrs)
            ))
        .add(Q("td")
          .style("vertical-align:top;width:5px")
          .add(sellWg()))))
  ;

};
