// Copyright 07-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Calendar hour widget.

import "fns";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Constructor.
///   hour  : 24 hours day hour.
///   minute: Minute.
///   action: Action to do when hour or minute changes.
/// \n, n, (\->()) -> <whour>
mk = \hour, minute, fn -> {
  Hlist = arr.fromIter(iter.map([0:24], \n ->
    return (n == hour ? "+" : "") + fns.format00(n);
  ));

  Mlist = arr.fromIter(iter.map([0:12], \n ->
    return (n * 5 == minute ? "+" : "") + fns.format00(n * 5);
  ));

  h = ui.select("wh", Hlist).on("change", \e -> fn(););
  m = ui.select("wm", Mlist).on("change", \e -> fn(););

  wg = Q("table")
    .add(Q("tr")
      .add(Q("td")
        .add(h))
      .add(Q("td")
        .add(Q("span")
          .html("<big> : </big>")))
      .add(Q("td")
        .add(m)))
  ;
  return {wg, h, m};
};


/// Returns the <whour> widget.
/// \<whour> -> <domo>
wg = \w -> return w.wg;;

/// Returns the hour of <whour>
/// \<whour> -> n
hour = \w -> return w.h.e.selectedIndex;;

/// Returns the minute of <whour>
/// \<whour> -> n
minute = \w -> return w.m.e.selectedIndex * 5;;
