// Copyright 01-Oct-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Songs management page.

import "libdm/menu";
import "data/cts";
import "data/danceSong";
import "fns";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo>, (\->()) -> ()
mk = async \wg, reload -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "DanceManagement",
    rq: "idata"
  });
  Groups = Rp.groups; // [s.]
  arr.sort(Groups, \g1, g2 -> return g1 < g2;);
  group = Rp.group;
  DanceSongs = arr.map(Rp.songs, danceSong.fromJs);
  arr.sort(DanceSongs, \S1, S2 -> return str.toUpper(S1.id) < str.toUpper(S2.id););

  // Control -------------------------------------------------------------------

  // \s -> ()
  changeGroup = async \g -> {
    if (g == group) return;
    await client.send({
      prg: cts.appName,
      source: "DanceManagement",
      rq: "changeGroup",
      group: g
    });
    mk(wg, reload);
  };

  // \s, n -> ()
  setLevel = async \song, level -> {
    await client.send({
      prg: cts.appName,
      source: "DanceManagement",
      rq: "setLevel",
      group,
      song,
      level
    });
  };

  // \s, n -> ()
  setSpeed = async \song, speed -> {
    await client.send({
      prg: cts.appName,
      source: "DanceManagement",
      rq: "setSpeed",
      group,
      song,
      speed
    });
  };

  // View ----------------------------------------------------------------------

  // \ -> [<domo>.]
  rows = \ -> {
    return arr.map(DanceSongs, \S -> return Q("tr")
        .add(Q("td")
          .att("title", II("Quality"))
          .style("width:5px")
          .klass("frame")
          .add(Q("table")
            .klass("main")
            .add(Q("tr")
              .add(Q("td")
                .add(Q("input")
                  .att("type", "radio")
                  .att("name", "level|" + S.id)
                  .checked(0 == S.level))
                  .on("click", \e -> setLevel(S.id, 0);))
              .add(Q("td")
                .html("?&nbsp;"))
              .add(Q("td")
                .add(Q("input")
                  .att("type", "radio")
                  .att("name", "level|" + S.id)
                  .checked(1 == S.level))
                  .on("click", \e -> setLevel(S.id, 1);))
              .add(Q("td")
                .html("&#8722;&nbsp;"))
              .add(Q("td")
                .add(Q("input")
                  .att("type", "radio")
                  .att("name", "level|" + S.id)
                  .checked(2 == S.level))
                  .on("click", \e -> setLevel(S.id, 2);))
              .add(Q("td")
                .html("+&nbsp;"))
                  )))
        .add(Q("td")
          .att("title", II("Speed"))
          .style("width:5px")
          .klass("frame")
          .add(Q("table")
            .klass("main")
            .add(Q("tr")
              .add(Q("td")
                .add(Q("input")
                  .att("type", "radio")
                  .att("name", "speed|" + S.id)
                  .checked(0 == S.speed))
                  .on("click", \e -> setSpeed(S.id, 0);))
              .add(Q("td")
                .html("&#171;&nbsp;"))
              .add(Q("td")
                .add(Q("input")
                  .att("type", "radio")
                  .att("name", "speed|" + S.id)
                  .checked(1 == S.speed))
                  .on("click", \e -> setSpeed(S.id, 1);))
              .add(Q("td")
                .html("&#187;&nbsp;"))
                  )))
        .add(Q("td")
          .klass("frame")
          .text(S.id))
        .add(Q("td")
          .klass("frame")
          .text(fns.formatInt(S.shortPlays, 2)))
        .add(Q("td")
          .klass("frame")
          .text(fns.formatInt(S.longPlays, 2)))
    ;);
  };

  Lopts = [];
  firstV = [true];
  for (g = Groups) {
    if (firstV!) firstV! = false;
    else arr.push(Lopts, menu.separator());

    arr.push(Lopts, menu.toption(g, g, \ -> changeGroup(g);));
  }
  Ropts = [
    menu.toption("_back_", II("Back"), reload)
  ];

  menuWg = menu.mk(Lopts, Ropts, group, false);

  wg
    .removeAll()
    .add(Q("div")
      .klass("head")
      .text(II("Songs Management")))
    .add(Q("hr"))
    .add(menuWg)
    .add(Q("table")
      .att("align", "center")
      .klass("frame3")
      .adds(rows()))
  ;
};

