// Copyright 26-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Dance selector page.

import "libdm/menu";
import "data/danceSong";
import "data/cts";
import "dance";
import "fns";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo>, b, (\->()) -> ()
mk = async \wg, isShort, reload -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "DanceSelector",
    rq: "idata"
  });

  Groups = Rp.groups; // [s.]
  arr.sort(Groups, \g1, g2 -> return str.toUpper(g1) < str.toUpper(g2););
  group = Rp.group;
  DanceSongs = arr.map(Rp.songs, danceSong.fromJs);
  arr.sort(
    DanceSongs, \S1, S2 -> return str.toUpper(S1.id) < str.toUpper(S2.id);
  );

  // Control -------------------------------------------------------------------

  // \s -> ()
  changeGroup = async \group -> {
    await client.send({
      prg: cts.appName,
      source: "DanceSelector",
      rq: "changeGroup",
      group
    });
    mk(wg, isShort, reload);
  };

  // \<keyboardEvend>, n -> ()
  keyInButton = \ev, isong -> {
    if (ev.key == "ArrowUp" & isong > 0) {
      Q("#bt:" + (isong - 1)).e.focus();
      ev.preventDefault();
      return;
    }
    if (ev.key == "ArrowDown" & isong < arr.size(DanceSongs) - 1) {
      Q("#bt:" + (isong + 1)).e.focus();
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowLeft") {
      ix = arr.index(Groups, \g -> return g == group;);
      if (ix <= 0) reload();
      else changeGroup(Groups[ix - 1]);

      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowRight") {
      ix0 = arr.index(Groups, \g -> return g == group;);
      ix = ix0 == -1 ? 0 : ix0;

      if (ix >= arr.size(Groups) - 1) reload();
      else changeGroup(Groups[ix + 1]);

      ev.preventDefault();
      return;
    }
  };

  // \s -> ()
  play = \song -> dance.mk(wg, isShort, group, song, reload);;

  // View ----------------------------------------------------------------------

  // \ -> [<domo>.]
  entries = \ -> {
    Rows = []; // [<domo>.]
    for (i, S = DanceSongs) {
      arr.push(Rows, Q("tr")
        .add(Q("td")
          .att("title", II("Quality"))
          .style("width:5px")
          .klass("frame")
          .text(S.level == 0 ? II("???")
              : S.level == 1 ? II("Ok")
                : II("Good")
            ))
        .add(Q("td")
          .att("title", II("Speed"))
          .style("width:5px")
          .klass("frame")
          .text(S.level == 0
            ? "???"
            : S.speed == 0
              ? II("Slow")
              : II("Fast")))
        .add(Q("td")
          .add(Q("button")
            .att("id", "bt:" + i)
            .style("width:100%;white-space:nowrap;text-align:left")
            .text(S.id))
            .on("keydown", \e -> keyInButton(e, i);)
            .on("click", \e -> play(S.id);))
        .add(Q("td")
          .klass("frame")
          .text(fns.formatInt(isShort ? S.shortPlays : S.longPlays, 2)))
      );
    }
    return Rows;
  };

  Lopts = [];
  firstV = [true];
  for (g = Groups) {
    if (firstV!) firstV! = false;
    else arr.push(Lopts, menu.separator());

    arr.push(Lopts, menu.toption(g, g, \ -> changeGroup(g);));
  }
  Ropts = [
    menu.toption("_back_", II("Back"), reload)
  ];
  menuWg = menu.mk(Lopts, Ropts, group, false);

  wg
    .removeAll()
    .add(Q("div")
      .klass("head")
      .text(isShort
        ? II("Short Dance Songs")
        : II("Long Dance Songs")))
    .add(Q("hr"))
    .add(menuWg)
   .add(Q("table")
      .att("align", "center")
      .klass("frame3")
      .adds(entries()))
  ;

  timer.delay(100, \ -> {
    mainButtonOp = null(Q("#bt:0"));
    if (mainButtonOp) mainButtonOp!.e.focus();
  });

};

