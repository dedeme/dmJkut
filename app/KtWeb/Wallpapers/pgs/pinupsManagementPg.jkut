// Copyright 26-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Pictures page.

import "cts";
import "global";
import "data/pict";
import "fns";
import "i18n";

Q = ui.q;
II = i18n.tlt;

// Pictures table rows.
prows = 4;

// Pictures table columns.
pcols = 5;

/// \<domo>, (\->()) -> ()
mk = async \wg, reload -> {
  dbKey, groups, group, // s,n,n
  :arr Picts, page, totalSights, shownSights // [<pict>.],n,n,n
  : await client.send({
    prg: cts.appName,
    source: "PinupsManagementPg",
    rq: "idata"
  });
  global.dbKeyV! = dbKey;

  psize = Picts.size();

  ptds = prows * pcols;
  maxPage = math.toInt((arr.size(Picts) - 1) / ptds);
  pg = page > maxPage ? maxPage : page;

  // Control -------------------------------------------------------------------

  // \s -> ()
  setGroup = async \group -> {
    await client.send({
      prg: cts.appName,
      source: "PinupsManagementPg",
      rq: "setGroup",
      dbKey: global.dbKeyV!,
      group
    });
    mk(wg, reload);
  };

  // \n -> ()
  setPage = async \page -> {
    await client.send({
      prg: cts.appName,
      source: "PinupsManagementPg",
      rq: "setPage",
      dbKey: global.dbKeyV!,
      page
    });
    mk(wg, reload);
  };

  // \s, n -> ()
  setLevel = async \id, level -> {
    await client.send({
      prg: cts.appName,
      source: "PinupsManagementPg",
      rq: "setLevel",
      dbKey: global.dbKeyV!,
      id,
      level
    });
    mk(wg, reload);
  };

  // \<KeyboardEvent> -> ()
  keyDown = async \ev -> {
    ev.preventDefault();
    switch (ev.key) {
      "ArrowUp": {
        await client.send({
          prg: cts.appName,
          source: "PinupsManagementPg",
          rq: "changePage",
          where: "up",
          dbKey: global.dbKeyV!
        });
        mk(wg, reload);
      }
    "ArrowDown": {
        ev.preventDefault();
        await client.send({
          prg: cts.appName,
          source: "PinupsManagementPg",
          rq: "changePage",
          where: "down",
          dbKey: global.dbKeyV!
        });
        mk(wg, reload);
      }
      "ArrowLeft": {
        ev.preventDefault();
        await client.send({
          prg: cts.appName,
          source: "PinupsManagementPg",
          rq: "changePage",
          where: "left",
          dbKey: global.dbKeyV!
        });
        mk(wg, reload);
      }
      "ArrowRight": {
        ev.preventDefault();
        await client.send({
          prg: cts.appName,
          source: "PinupsManagementPg",
          rq: "changePage",
          where: "right",
          dbKey: global.dbKeyV!
        });
        mk(wg, reload);
      }
    }
  };

  // View ----------------------------------------------------------------------

  // \n -> ()
  showPict = \id ->
    wg
    .removeAll()
    .add(Q("table")
      .att("align", "center")
      .add(Q("tr")
        .add(Q("td")
          .add(ui.img("pinups/" + id)
            .klass("frame")
            .style("width:800px;text-align:center")
            .on("click", \e -> mk(wg, reload);)))))
    ;;

  // \<pict> -> <domo>
  tdPict = \:pict p -> {
    Sels = []; // [<domo.]
    for (i = cts.minPictLevel:cts.maxPictLevel + 1) {
      Sels.push(Q("td")
        .add(Q("table")
          .klass("main")
          .add(Q("tr")
            .add(Q("td")
              .add(Q("input")
                .att("type", "radio")
                .att("name", p.id)
                .checked(i == p.level))
                .on("click", \e -> setLevel(p.id, i);))
            .add(Q("td")
              .add(Q("span")
                .text("" + i)))))
      );
    }
    return Q("td")
      .style("text-align:center")
      .add(ui.img("pinups/" + p.id)
        .klass("frame")
        .style("width:175px")
        .att("title", p.id)
        .on("click", \e -> showPict(p.id);))
      .add(Q("table")
        .att("align", "center")
        .add(Q("tr")
          .adds(Sels)))
    ;
  };

  // \ -> [<domo>.]
  rows = \ -> {
    R = []; // [<domo>.]
    npV = [pg * pcols * prows];
    for (row = 0:prows) {
      tr = Q("tr");
      for (col = 0:pcols) {
        if (npV! < psize) {
          tr.add(tdPict(Picts[npV!]));
        } else {
          tr.add(Q("td"));
        }
        npV! += 1;
      }
      R.push(tr);
    }
    return R;
  };

  // \ -> [<domo>.]
  pagesWg = \ -> {
    Trs = []; // [<domo>.]
    Tds = []; // [[<domo>.].]
    max = maxPage * ptds < psize ? maxPage + 1 : maxPage;
    for (i = 0:max) {
      first = i * ptds;
      last0 = first + ptds - 1;
      last = last0 >= psize ? psize - 1 : last0;
      for (i = 0:3) Tds.push([]);
      arr.push(Tds[0], Q("td")
        .style("text-align:center;color:#c9c9c9")
        .text(str.toUpper(Picts[first][pict.id][:2])));
      arr.push(Tds[1], Q("td")
        .klass(i == pg ? "frame3": "frame")
        .style("cursor:pointer")
        .text(fns.formatInt(i + 1, 2))
        .on("click", \e -> setPage(i);)
      );
      arr.push(Tds[2], Q("td")
        .style("text-align:center;color:#c9c9c9")
        .text(str.toUpper(Picts[last][pict.id][:2])));
    }

    for (i = 0:3) Trs.push(Q("tr").adds(Tds[i]));
    return Trs;
  };

  window.document.onkeydown = keyDown;

  wg
    .removeAll()
    .add(Q("div")
      .klass("head")
      .text(i18n.fmt(
          II("Pinups Management [ %0 / %1 ]"),
          [shownSights, totalSights]
        )))
    .add(Q("table")
      .att("align", "center")
      .add(Q("tr")
        .add(Q("td")
          .add(ui.link(\e -> reload();)
            .klass("link")
            .text("[ " + II("Back") + " ]")))))
    .add(Q("table")
      .klass("main")
      .add(Q("tr")
        .add(Q("td")
          .add(Q("table")
            .att("align", "left")
            .add(Q("tr")
              .add(Q("td")
                .att("colspan", groups)
                .style("width:150px")
                .add(ui.hrule(II("Groups"), 25)))
              .add(Q("td")
                .style("width:100px")
                .add(ui.hrule(II("Sights"), 25))))
            .add(Q("tr")
              .adds(iter.map([0:groups], \i -> return Q("td")
                  .klass(i == group ? "frame3": "frame")
                  .style("cursor:pointer;text-align:center")
                  .text("" + i)
                  .on("click", \e -> setGroup(i);)
                ;))
              .add(Q("td")
                .klass("frame3")
                .style("text-align:center")
                .text("" + (Picts.reduce(
                    0, \sum, :pict p -> return sum + p.level;
                  )))))
            .add(Q("tr")
              .add(Q("td")
                .att("colspan", groups)
                .add(Q("hr")))
              .add(Q("td")
                .add(Q("hr"))))))
        .add(Q("td")
          .add(Q("table")
            .att("align", "right")
            .adds(pagesWg())))))
    .add(Q("div")
      .style("padding-top:10px;"))
    .add(Q("table")
      .klass("main")
      .adds(rows()))
  ;
};

