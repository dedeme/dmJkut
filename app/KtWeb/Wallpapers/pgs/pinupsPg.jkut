// Copyright 27-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Songs page.

import "cts";
import "global";
import "data/pict";
import "data/song";
import "wgs/clocksWg";
import "wgs/infoWg";
import "wgs/pictTimeWg";
import "media";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo>, (\->()) -> ()
mk = async \wg, reload -> {
  dbKey, :pict picture, radioName, radioUrl // s,<pict>,s,s
  : await client.send({
    prg: cts.appName,
    source: "PinupsPg",
    rq: "idata"
  });
  global.dbKeyV! = dbKey;

  pictV = [picture];

  pictTm = timer.mk(media.picturesTime);

  Visuals = media.visuals();

  img = Visuals.img
    .att("src", "img/pinups/" + picture.id)
  ;

  Clocks = clocksWg.mk(Visuals.tm);

  audio = new Audio();
  audio.src = radioUrl;
  audio.controls = true;
  audio.volume = media.volume;
  audio.autoplay = true;


  InfoPicture = infoWg.mk(-485, infoWg.pictureWg("-", picture));
  InfoSong = infoWg.mk(
    -480,
    infoWg.songWg("Radio", song.mk(0, 0, radioName, 0), audio)
  );
  PictTime = pictTimeWg.mk(-480);

  goBackCallsV = [0];

  // Control -------------------------------------------------------------------

  // \<event> -> ()
  goBack = \e -> {
    if (goBackCallsV! > 0) {
      reload();
      return;
    }

    timer.stop(pictTm);
    goBackCallsV! += 1;
    media.fadeOut(false, audio, media.fadeOutSongEnd);
    timer.delay(math.toInt(media.fadeOutSongEnd), reload);
  };

  // \<KeyboardEvent> -> ()
  keyDown = \ev -> {
    if (ev.key == "ArrowUp") {
      goBack(ev);
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowDown") {
      InfoPicture.changeOpacity();
      InfoSong.changeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowLeft") {
      Clocks.clockChangeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowRight") {
      Clocks.chronChangeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key >= "1" & ev.key <= "6") {
      PictTime.show(math.fromStr(ev.key)!);
      ev.preventDefault();
      return;
    }
  };

  // View ----------------------------------------------------------------------

  div = Visuals.div
    .setStyle(
      "background-image",
      "url('img/pinups/" + picture.id + "')"
    )
    .add(InfoPicture.wg)
    .add(InfoSong.wg)
    .add(PictTime.wg)
    .on("click", goBack)
  ;

  Q("@body")
    .on("keydown", keyDown);

  wg
    .removeAll()
    .add(div)
  ;

  div.e.requestFullscreen();
  timer.run (pictTm, async \ -> {
    dbKey, :pict picture // s,<pict>
    : await client.send({
      prg: cts.appName,
      source: "PinupsPg",
      rq: "pictData"
    });
    global.dbKeyV! = dbKey;
    :pict p = pictV!;
    if (picture.id != p.id) {
      pictV! = picture;
      media.changePinup(div, img, InfoPicture, picture);
    }
  });
};

