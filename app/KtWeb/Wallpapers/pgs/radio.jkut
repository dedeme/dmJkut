// Copyright 27-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Songs page.

import "data/cts";
import "data/pict";
import "data/song";
import "wgs/clocks";
import "wgs/info";
import "wgs/pictTime";
import "media";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo>, (\->()) -> ()
mk = async \wg, reload -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "Radio",
    rq: "idata"
  });

  group = Rp.group;
  groupV = [group];
  Pict = pict.fromJs(Rp.pict);
  PictV = [Pict];
  radioName = Rp.radioName;
  radioUrl = Rp.radioUrl;


  pictTm = timer.mk(media.picturesTime);

  Visuals = media.visuals();

  img = Visuals.img
    .att("src", "img/fondosEscritorio/" + group + "/" + Pict.id)
  ;

  Clocks = clocks.mk(Visuals.tm);

  audio = new Audio();
  audio.src = radioUrl;
  audio.controls = true;
  audio.volume = media.volume;
  audio.autoplay = true;


  InfoPicture = info.mk(-485, info.pictureWg(group, Pict));
  InfoSong = info.mk(
    -480,
    info.songWg("Radio", song.mk(0, 0, 0, radioName), audio)
  );
  PictTime = pictTime.mk(-480);

  goBackCallsV = [0];

  // Control -------------------------------------------------------------------

  // \<event> -> ()
  goBack = \e -> {
    if (goBackCallsV! > 0) {
      reload();
      return;
    }

    timer.stop(pictTm);
    goBackCallsV! += 1;
    media.fadeOut(false, audio, media.fadeOutSongEnd);
    timer.delay(math.toInt(media.fadeOutSongEnd), reload);
  };

  // \<KeyboardEvent> -> ()
  keyDown = \ev -> {
    if (ev.key == "ArrowUp") {
      goBack(ev);
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowDown") {
      InfoPicture.changeOpacity();
      InfoSong.changeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowLeft") {
      Clocks.clockChangeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowRight") {
      Clocks.chronChangeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key >= "1" & ev.key <= "6") {
      PictTime.show(math.fromStr(ev.key)!);
      ev.preventDefault();
      return;
    }
  };

  // View ----------------------------------------------------------------------

  div = Visuals.div
    .setStyle(
      "background-image",
      "url('img/fondosEscritorio/" + group + "/" + Pict.id + "')"
    )
    .add(InfoPicture.wg)
    .add(InfoSong.wg)
    .add(PictTime.wg)
    .on("click", goBack)
  ;

  Q("@body")
    .on("keydown", keyDown);

  wg
    .removeAll()
    .add(div)
  ;

  div.e.requestFullscreen();
  timer.run (pictTm, async \ -> {
    Rp = await client.send({
      prg: cts.appName,
      source: "Songs",
      rq: "pictData"
    });
    newGroup = Rp.group;
    NewPict = pict.fromJs(Rp.pict);
    if (newGroup != groupV! | NewPict.id != PictV!.id) {
      groupV! = newGroup;
      PictV! = NewPict;
      media.changePict(div, img, InfoPicture, newGroup, NewPict);
    }
  });
};

