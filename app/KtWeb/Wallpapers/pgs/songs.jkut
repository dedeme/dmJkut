// Copyright 27-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Songs page.

import "data/cts";
import "data/pict";
import "data/song";
import "wgs/clocks";
import "wgs/info";
import "wgs/pictTime";
import "media";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo>, (\->()) -> ()
mk = async \wg, reload -> {
  Rp = await client.send({
    prg: cts.appName,
    source: "Songs",
    rq: "idata"
  });

  group = Rp.group;
  groupV = [group];
  Pict = pict.fromJs(Rp.pict);
  PictV = [Pict];
  songGroup = Rp.songGroup;
  songGroupV = [songGroup];
  Song = song.fromJs(Rp.song);
  SongV = [Song];

  pictTm = timer.mk(media.picturesTime);
  songTm = timer.mk(media.songsTime);

  Visuals = media.visuals();

  img = Visuals.img
    .att("src", "img/fondosEscritorio/" + group + "/" + Pict.id)
  ;

  Clocks = clocks.mk(Visuals.tm);

  audio = new Audio();
  audio.src = "songs/" + songGroup + "/" + Song.id;
  audio.controls = true;
  audio.volume = media.volume;
  audioLoadedV = [false];
  timer.delay (1000, \ -> {
    tm1 = timer.mk(500);
    timer.run(tm1, \ -> {
      d = audio.duration - 0.1;
      audio.currentTime = d;
      if (d == audio.currentTime | audio.currentTime == 0) {
        timer.stop(tm1);
        tm2 = timer.mk(500);
        t = (audio.duration * Song.lapse) / 100.0;
        timer.run(tm2, \ -> {
          audio.currentTime = t;
          if (t == audio.currentTime | audio.currentTime == 0) {
            audio.play();
            timer.stop(tm2);
            audioLoadedV! = true;
          }
        });
      }
    });
  });

  InfoPicture = info.mk(-485, info.pictureWg(group, Pict));
  InfoSong = info.mk(-480, info.songWg(songGroup, Song, audio));
  PictTime = pictTime.mk(-480);

  goBackCallsV = [0];

  // Control -------------------------------------------------------------------

  // \<event> -> ()
  goBack = \e -> {
    if (goBackCallsV! > 0) {
      reload();
      return;
    }

    timer.stop(pictTm);
    timer.stop(songTm);
    goBackCallsV! += 1;
    media.fadeOut(false, audio, media.fadeOutSongEnd);
    timer.delay(math.toInt(media.fadeOutSongEnd), reload);
  };

  // \<KeyboardEvent> -> ()
  keyDown = \ev -> {
    if (ev.key == "ArrowUp") {
      goBack(ev);
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowDown") {
      InfoPicture.changeOpacity();
      InfoSong.changeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowLeft") {
      Clocks.clockChangeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key == "ArrowRight") {
      Clocks.chronChangeOpacity();
      ev.preventDefault();
      return;
    }

    if (ev.key >= "1" & ev.key <= "6") {
      PictTime.show(math.fromStr(ev.key)!);
      ev.preventDefault();
      return;
    }
  };

  // View ----------------------------------------------------------------------

  div = Visuals.div
    .setStyle(
      "background-image",
      "url('img/fondosEscritorio/" + group + "/" + Pict.id + "')"
    )
    .add(InfoPicture.wg)
    .add(InfoSong.wg)
    .add(PictTime.wg)
    .on("click", goBack)
  ;

  Q("@body")
    .on("keydown", keyDown);

  wg
    .removeAll()
    .add(div)
  ;

  div.e.requestFullscreen();
  firstPictureV = [true];
  timer.run (pictTm, async \ -> {
    if (!firstPictureV! & !audioLoadedV!) return;
    firstPictureV! = false;

    Rp = await client.send({
      prg: cts.appName,
      source: "Songs",
      rq: "pictData"
    });
    newGroup = Rp.group;
    NewPict = pict.fromJs(Rp.pict);
    if (newGroup != groupV! | NewPict.id != PictV!.id) {
      groupV! = newGroup;
      PictV! = NewPict;
      media.changePict(div, img, InfoPicture, newGroup, NewPict);
    }
  });

  timer.run (songTm, async \ -> {
    if (audio.ended) {
      audio.pause();
      audioLoadedV! = false;
      Rp = await client.send({
        prg: cts.appName,
        source: "Songs",
        rq: "newSong"
      });
      songGroup = Rp.group;
      songGroupV! = songGroup;
      Song = song.fromJs(Rp.song);
      SongV! = Song;
      audio.src = "songs/" + songGroup + "/" + Song.id;
      InfoSong.changeContent(info.songWg(songGroup, Song, audio));
      timer.delay (1000, \ -> {
        tm1 = timer.mk(50);
        timer.run(tm1, \ -> {
          d = audio.duration - 0.1;
          audio.currentTime = d;
          if (d == audio.currentTime | audio.currentTime == 0) {
            timer.stop(tm1);
            tm2 = timer.mk(50);
            t = (audio.duration * Song.lapse) / 100.0;
            timer.run(tm2, \ -> {
              audio.currentTime = t;
              if (t == audio.currentTime | audio.currentTime == 0) {
                audio.play();
                timer.stop(tm2);
                audioLoadedV! = true;
              }
            });
          }
        });
      });
    } else if (audioLoadedV! == true) {
      client.send({
        prg: cts.appName,
        source: "Songs",
        rq: "setLapse",
        group: songGroupV!,
        song: SongV!.id,
        lapse: audio.currentTime * 100 / audio.duration
      });
    }
  });
};

