// Copyright 26-Sep-2023 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Information widget.

import "cts";
import "global";
import "data/pict";
import "data/song";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// Constructor.
///   padding    : Content padding.
///   containedWg: Content to show.
///   RETURN: {wg:<domo>, changeOpacity:(\->()), changeContent:(\<domo>->())}
/// \n, <domo> -> <info>
///-
mk = \padding, continedWg -> {
  content = Q("div")
    .add(continedWg)
  ;
  wg = Q("div")
    .style(
        "z-index:3;" +
        "position:relative;" +
        "top: " + padding + "px;" +
        "left:15px;" +
        "opacity:0;" +
        "transition: opacity 2s linear;"
      )
    .add(content)
  ;

  // \ -> ()
  changeOpacity = \ -> {
    isHidden = wg.e.style.getPropertyValue("opacity") == "0";
    wg.setStyle("opacity", isHidden ? "1" : "0");
  };

  // \<domo> -> ()
  changeContent = \newContent ->
    content.removeAll().add(newContent);;

  return {wg, changeOpacity, changeContent};
};

/// Returns picture information widget.
/// \s, <pict> -> <domo>
pictureWg = \group, :pict p -> {
  levelV = [p.level];
  sightsWg = Q("div")
    .style("cursor:pointer")
    .text(p.sights + " / " + levelV!)
  ;

  // \<mouseEvent> -> ()
  changeLevel = async \ev -> {
    ev.preventDefault();
    ev.stopPropagation();

    levelV! += 1;
    if (levelV! > cts.maxPictLevel) levelV! = cts.minPictLevel;

    , dbKey : await client.send({
      prg: cts.appName,
      source: "InfoWg",
      rq: "changePictLevel",
      dbKey: global.dbKeyV!,
      group,
      id: p.id,
      level: levelV!
    });
    global.dbKeyV! = dbKey;

    sightsWg
      .text(p.sights + " / " + levelV!);
  };

  sightsWg.on("click", changeLevel);

  tb = Q("table")
    .klass("frame3")
    .add(Q("tr")
      .add(Q("td")
        .att("colspan", "3")
        .add(ui.hrule(II("Picture"), 50))))
    .add(Q("tr")
      .add(Q("td")
        .style("text-align:center")
        .text(II("Group")))
      .add(Q("td")
        .style("padding-left:5px")
        .text(II("Name")))
      .add(Q("td")
        .style("text-align:center")
        .text(II("Sights"))))
    .add(Q("tr")
      .add(Q("td")
        .klass("frame")
        .style("width:45px;white-space:nowrap;text-align:center")
        .text(group))
      .add(Q("td")
        .klass("frame")
        .style("max-width: 500px;white-space:nowrap;overflow: hidden;")
        .text(p.id))
      .add(Q("td")
        .style("width:45px;white-space:nowrap;text-align:center")
        .klass("frame")
        .add(sightsWg)))
  ;
  tb.on("click", \e -> {
    e.preventDefault();
    e.stopPropagation();
  });
  return tb;
};

/// Returns song information widget.
/// \s, <song>, <audio> -> <domo>
songWg = \songGroup, :song s, audio -> {
  levelV = [s.level];
  nameWg = Q("div")
    .text(s.id)
  ;
  sightsWg = s.level == 0
    ? Q("div")
      .style("cursor:pointer")
      .text("· >> ·")
    : Q("div")
      .style("cursor:pointer")
      .text(s.sights + " / " + levelV!)
  ;

  // \<mouseEvent> -> ()
  changeLevel = async \ev -> {
    ev.preventDefault();
    ev.stopPropagation();

    levelV! += 1;
    if (levelV! > cts.maxSongLevel) levelV! = cts.minSongLevel;

    , dbKey : await client.send({
      prg: cts.appName,
      source: "InfoWg",
      rq: "changeSongLevel",
      dbKey: global.dbKeyV!,
      group: songGroup,
      id: s.id,
      level: levelV!
    });
    global.dbKeyV! = dbKey;

    sightsWg
      .text(s.sights + " / " + levelV!);
  };

  // \<mouseEvent> -> ()
  changeRadio = async \ev -> {
    ev.preventDefault();
    ev.stopPropagation();

    dbKey, radioName, radioUrl // s,s,s
    : await client.send({
      prg: cts.appName,
      source: "InfoWg",
      rq: "changeRadio",
      dbKey: global.dbKeyV!
    });
    global.dbKeyV! = dbKey;

    nameWg
      .text(radioName);
    audio.src = radioUrl;
  };

  if (s.level > 0) sightsWg.on("click", changeLevel);
  else sightsWg.on("click", changeRadio);

  tb = Q("table")
    .klass("frame3")
    .add(Q("tr")
      .add(Q("td")
        .att("colspan", "3")
        .add(ui.hrule(II("Song"), 50))))
    .add(Q("tr")
      .add(Q("td")
        .style("text-align:center")
        .text(II("Group")))
      .add(Q("td")
        .style("padding-left:5px")
        .text(II("Name")))
      .add(Q("td")
        .style("text-align:center")
        .text(II("Sights"))))
    .add(Q("tr")
      .add(Q("td")
        .klass("frame")
        .style("width:45px;white-space:nowrap;text-align:center")
        .text(songGroup))
      .add(Q("td")
        .klass("frame")
        .style("max-width: 500px;white-space:nowrap;overflow: hidden;")
        .add(nameWg))
      .add(Q("td")
        .style("width:45px;white-space:nowrap;text-align:center")
        .klass("frame")
        .add(sightsWg)))
    .add(Q("tr")
      .add(Q("td")
        .att("colspan", "3")
        .style("text-align:center")
        .add(Q(audio))))
  ;
  tb.on("click", \e -> {
    e.preventDefault();
    e.stopPropagation();
  });
  return tb;
};


/// Returns dance information widget.
/// 'tm' is in seconds.
/// \s, s, n, <audio> -> <domo>
danceWg = \songGroup, ssong, tm, audio -> {
  return Q("table")
    .klass("frame3")
    .add(Q("tr")
      .add(Q("td")
        .att("colspan", "3")
        .add(ui.hrule(II("Dance Song"), 50))))
    .add(Q("tr")
      .add(Q("td")
        .style("text-align:center")
        .text(II("Group")))
      .add(Q("td")
        .style("padding-left:5px")
        .text(II("Name")))
      .add(Q("td")
        .style("text-align:center")
        .text(II("Time"))))
    .add(Q("tr")
      .add(Q("td")
        .klass("frame")
        .style("width:45px;white-space:nowrap;text-align:center")
        .text(songGroup))
      .add(Q("td")
        .klass("frame")
        .style("max-width: 500px;white-space:nowrap;overflow: hidden;")
        .text(ssong))
      .add(Q("td")
        .style("width:45px;white-space:nowrap;text-align:center")
        .klass("frame")
        .text(tm + "'")))
    .add(Q("tr")
      .add(Q("td")
        .att("colspan", "3")
        .style("text-align:center")
        .add(Q(audio))))
  ;
};


