// Copyright 29-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Percentages page

import "libdm/menu";
import "data/cts";
import "data/fns";
import "data/hotMap";
import "data/paramsEval";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo> -> ()
mk = async \wg -> {
  Url = ui.url();
  model = dic.hasKey(Url, "2") ? Url["2"] : "";
  Rp = await client.send({
    prg: cts.appName,
    module: "MMarket",
    source: "HotMapsPg",
    rq: "idata",
    model
  });
  modelSel = Rp.model;
  // [s...]
  Models = Rp.models;
  MapsGroup = arr.map(Rp.mapsGroup, hotMap.fromJs);

  // View ----------------------------------------------------------------------

  // [<paramsEval>...] -> <domo>
  twoChart = \Es -> {
    firstParam = Es[0].Params[0];
    cols = arr.size(arr.takeWhile(Es, \E -> return E.Params[0] == firstParam;));
    rows = math.toInt(arr.size(Es) / cols);
    nfmt0 = fns.paramFormatter(Es[0].Params[0], Es[cols].Params[0]);
    nfmt1 = fns.paramFormatter(Es[0].Params[1], Es[1].Params[1]);
    max = arr.reduce(Es, Es[0].ev, \r, E -> return E.ev > r ? E.ev : r;);
    min = arr.reduce(Es, Es[0].ev, \r, E -> return E.ev < r ? E.ev : r;);
    color = fns.valueColor(max, min);

    return Q("table")
      .klass("flat")
      .adds(arr.fromIter(iter.map([0:rows], \row -> return Q("tr")
          .adds(arr.fromIter(iter.map([0:cols], \col ->  {
              i = row * cols + col;
              E = Es[i];
              return Q("td")
                .style(
                    "padding:0px;" +
                    "width: 6px; height: 6px;" +
                    "background: " + color(E.ev)
                  )
                .att(
                    "title",
                    nfmt0(E.Params[0]) + " - " +
                    nfmt1(E.Params[1]) + "\n" +
                    math.toIso(E.ev / 100, 2)
                  )
              ;
            })))
        ;)))
    ;
  };

  // [<paramsEval>...] -> <domo>
  oneChart = \Es -> {
    nfmt = fns.paramFormatter(Es[0].Params[0], Es[1].Params[0]);
    max = arr.reduce(Es, Es[0].ev, \r, E -> return E.ev > r ? E.ev : r;);
    min = arr.reduce(Es, Es[0].ev, \r, E -> return E.ev < r ? E.ev : r;);
    color = fns.valueColor(max, min);

    return Q("table")
      .klass("flat")
      .adds(arr.map(Es, \E -> return Q("tr")
        .add(Q("td")
          .style(
              "padding:0px;" +
              "width: 120px; height: 6px;" +
              "background: " + color(E.ev)
            )
          .att(
              "title",
              nfmt(E.Params[0]) + "\n" +
              math.toIso(E.ev / 100, 2)
            ));))
      ;
  };

  // \<hotMap> -> <domo>
  mapChart = \Map ->
    return Q("table")
      .klass("flat")
      .add(Q("tr")
        .add(Q("td")
          .klass("frame")
          .style("text-align:center")
          .text(time.toIso(time.fromStr(Map.date)))))
      .add(Q("tr")
        .add(Q("td")
          .add(arr.size(Map.Entries[0].Params) == 1
            ? oneChart(Map.Entries)
            : twoChart(Map.Entries))))
    ;;

  // \n, n -> <domo>
  rowGroups = \start, end ->
    return Q("table")
      .att("align", "center")
      .add(Q("tr")
        .adds(arr.fromIter(iter.map([start:end],  \i ->
          return Q("td").add(mapChart(MapsGroup[i]));
        ))))
    ;;

  Lopts = [];
  for (M = Models) {
    arr.push(Lopts, menu.separator());
    arr.push(Lopts, menu.tlink(M, M, ["mmarket&hotmaps"]));
  }
  arr.shift(Lopts);
  menuWg = menu.mk(Lopts, [], modelSel, false);


  groups = arr.size(MapsGroup);
  wg
    .removeAll()
    .add(menuWg)
    .add(rowGroups(0, groups >= 4 ? 4 : groups))
  ;

  if (groups >= 4)
    wg
      .add(rowGroups(4, groups >= 8 ? 8 : groups))
    ;

  if (groups >= 8)
    wg
      .add(Q("hr"))
      .add(rowGroups(8, groups >= 13 ? 13 : groups))
    ;

  if (groups >= 13)
    wg
      .add(rowGroups(13, groups >= 18 ? 18 : groups))
    ;

  if (groups >= 18)
    wg
      .add(Q("hr"))
      .add(rowGroups(18, groups))
    ;

};
