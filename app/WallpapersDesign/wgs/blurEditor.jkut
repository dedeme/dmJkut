// Copyright 30-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Editor of blur operation.

import "libdm/modalBox";
import "data/cts";
import "data/image";
import "data/imgBlur";
import "i18n";

Q = ui.q;
II = i18n.tlt;

/// \<domo>, <image>, (\->()) -> ()
mk = \ImgDiv, Image, onChange -> {
  Blur = Image.Blur;
  IsActivated = [Blur ? true : false];

  // \s, s, n -> <domo>
  mkIn = \id, nextId, value -> return ui.changePoint(ui.field(nextId)
      .att("id", id)
      .style("width:80px")
      .value(value)
    );;

  Ratio = mkIn("ratio", "ratio", IsActivated! ? Blur!.ratio : cts.ratioBlur);

  EditorDiv = Q("div");

  MkEditor = [[]];

  // Control -------------------------------------------------------------------

  // \b -> ()
  activate = \value -> {
    IsActivated! = value;
    MkEditor!(EditorDiv);
  };

  // \<event> -> ()
  restore = \ev -> {
    Blur = Image.Blur;
    IsActivated = [Blur ? true : false];

    Ratio.value(IsActivated! ? Blur.ratio : cts.ratioBlur);

    onChange(Image);
    MkEditor!(EditorDiv);
  };

  // \<event> -> ()
  update = \ev -> {
    // \<domo> -> n
    value = \I -> {
      R = math.fromStr(I.getValue());
      if (!R) {
        arr.push(R, cts.ratioBlur);
        I.value("" + R!);
      }
      return R!;
    };
    Blur = IsActivated!
      ? [imgBlur.mk(value(Ratio))]
      : []
    ;

    onChange(image.setBlur(Image, Blur));
    MkEditor!(EditorDiv);
  };

  // \<event> -> ()
  close = \ev -> modalBox.show(cts.Box, false);;

  // View ----------------------------------------------------------------------

  // \<domo> -> ()
  MkEditor! = \Div -> {
    Ratio.disabled(!IsActivated!);
    Div
      .removeAll()
      .add(Q("table")
        .att("align", "center")
        .add(Q("tr")
          .add(Q("td")
            .att("colspan", 3)
            .add(IsActivated!
                ? ui.link(\ev -> activate(false);)
                    .klass("link")
                    .text(II("Deactivate"))
                : ui.link(\ev -> activate(true);)
                    .klass("link")
                    .text(II("Activate"))
              )))
        .add(Q("tr")
          .add(Q("td")
            .style("text-align:center")
            .text(II("Percentage (0 - 100)"))))
        .add(Q("tr")
          .add(Q("td")
            .add(Ratio))))
    ;
  };

  MkEditor!(EditorDiv);

  cts.BoxContent
    .removeAll()
    .add(Q("table")
      .klass("main")
      .add(Q("tr")
        .add(Q("td"))
        .add(Q("td")
          .style("text-align:right")
          .add(ui.link(close)
            .klass("link")
            .text(II("Close")))))
      .add(Q("tr")
        .add(Q("td")
          .style("text-align:left")
          .add(ui.link(update)
            .klass("link")
            .text(II("Update")))
          .add(Q("span")
            .html("&nbsp;&nbsp;"))
          .add(ui.link(restore)
            .klass("link")
            .text(II("Restore"))))
        .add(Q("td"))))
    .add(Q("hr"))
    .add(EditorDiv)
    .add(Q("hr"))
    .add(ImgDiv)
  ;

};
